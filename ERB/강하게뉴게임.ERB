@강하게뉴게임
;팬티 보관고. 200 인분 30 종류까지 대응이 뛰어나고 것
#DIM 팬티콜렉션, 200,40
#DIM PREV_MASTER
#DIM RETAIN_CFLAG, 150
#DIM 인계캐릭

;주회전의 성장분을 기록
;매주당신 커스텀 해 체력 기력 2500 스타트 하면(자) 주회마다+500가 될 생각이 들지만
;바이처 더스트의 해금일부터 해 그런 굉장한 효율로도 되지 않아이겠지 감
;이쪽이 고민해도 디버그 콘솔이나 ERB조금 고쳐 쓸 수 있으면(자) 손다 키군요 해
PREV_MASTER = FLAG:역극

VARSET JUEL
VARSET MOBGIRL_PLACE, 99

;사용 LOCAL 배열 요소
;5：역극 캐릭터의 출금상태
;6：지금 주회의 캐릭터 인원수(CHARANUM 퇴피용)

;FLAG 리셋
;남겨지는 FLAG
;11：주회수
;560：발원함 확인용. 확인 후 리셋
;561：정력강화회수
;563：중혼가능인원수(미사용)
;564-567：연모/연인특전(미사용)
;700：팬티콜렉션(종류수)
;701-702：실적 해제 관계 + 810
;850：청소중독 치료 회수
;1007：생리주기가시화의 ON/OFF
;1028：짐작모드
;1030：역극

CALL BUFF_RESET

FOR LOCAL, 0,1500
	SELECTCASE LOCAL
	CASE 11, 22, 23, 560,561,563 TO 567,700 TO 709,810,850,1001 TO 1999
		CONTINUE
	CASEELSE
		FLAG:LOCAL = 0
	ENDSELECT
NEXT

;역극 캐릭터의 출금상태를 체크, 출금플래그는 LOCAL:5에 보존
SIF FLAG:역극
	LOCAL:5 = CFLAG:(FLAG:역극):출금

CFLAG:MASTER:초대 = 0
CFLAG:MASTER:육아인원수 = 0
CFLAG:MASTER:아이인원수 = 0
FLAG:주회수 ++
FOR LOCAL, 1, CHARANUM
	RETAIN_CFLAG:LOCAL = CFLAG:LOCAL:구상용이어받아테두리
	IF TALENT:LOCAL:연인 || GETBIT(CFLAG:LOCAL:인계, 1)
		SETBIT TFLAG:(LOCAL + 500), 1
	ELSEIF TALENT:LOCAL:연모 || GETBIT(CFLAG:LOCAL:인계, 0)
		SETBIT TFLAG:(LOCAL + 500), 0
	ENDIF
	SIF TALENT:LOCAL:자위중독 || GETBIT(CFLAG:LOCAL:인계, 2)
		SETBIT TFLAG:(LOCAL + 500), 2
	SIF TALENT:LOCAL:음호 || GETBIT(CFLAG:LOCAL:인계, 3)
		SETBIT TFLAG:(LOCAL + 500), 3
	SIF TALENT:LOCAL:항문광 || GETBIT(CFLAG:LOCAL:인계, 4)
		SETBIT TFLAG:(LOCAL + 500), 4
	SIF TALENT:LOCAL:음유 || GETBIT(CFLAG:LOCAL:인계, 5)
		SETBIT TFLAG:(LOCAL + 500), 5
	SIF TALENT:LOCAL:키스마 || GETBIT(CFLAG:LOCAL:인계, 6)
		SETBIT TFLAG:(LOCAL + 500), 6
	SIF GETBIT(TALENT:LOCAL:연인, 1) || GETBIT(CFLAG:LOCAL:인계, 7)
		SETBIT TFLAG:(LOCAL + 500), 7
	FOR LOCAL:1, 0,40
		팬티콜렉션 :LOCAL:(LOCAL:1) = CFLAG:LOCAL:(LOCAL:1 + 100)
	NEXT
NEXT
;리셋이전캐릭터 인원수를 LOCAL:6에 앞둔다.
LOCAL:6 = CHARANUM

;;
CALL SET_인계여부
;당신 이외 전원 리셋.
;여기서 CHARANUM의 값이 바뀌므로 주의
;;PICKUPCHARA 0
인계캐릭 =1
REPEAT (LOCAL:6)-1
	IF GETBIT(CFLAG:인계캐릭:인계, 8)
	
		;;인계 캐릭에 따로 처리할것
		CFLAG:인계캐릭:저택에거주=0
	

		인계캐릭 ++
		CONTINUE
	ENDIF
	DELCHARA 인계캐릭
REND

;리셋이전캐릭터 인원수로 FOR문을 돌린다
FOR LOCAL, 1, LOCAL:6
	;CSV 파일의 존재 판정
	IF EXISTCSV(LOCAL)
		IF GETCHARA (LOCAL) == -1
			ADDCHARA LOCAL
			TARGET = LOCAL
			RESULT = 0
			SORTCHARA
			TRYCALLFORM M_KOJO_K{NO:LOCAL}
			SIF RESULT
				CFLAG:LOCAL:대사실장상황 = 1
		ENDIF
		;효율화 추구를 위해 인계 플래그와 동시에 돌린다
		;파일 체크는 벌써 하고 있기 때문에 그 안에서 돌리면 끝나는 이야기였습니다(반성)
		FOR LOCAL:1, 0,40
			;인계 플래그세트
			SIF LOCAL:1 < 9 && GETBIT (TFLAG:(LOCAL + 500), LOCAL:1)
				SETBIT CFLAG:LOCAL:인계, LOCAL:1
			;팬티콜렉션의 인계
			CFLAG:LOCAL:(LOCAL:1 + 100) = 팬티콜렉션 :LOCAL:(LOCAL:1)
			CFLAG:LOCAL:구상용이어받아테두리 = RETAIN_CFLAG:LOCAL
		NEXT
	ENDIF
NEXT


;타겟·어시스턴트 초기화, 대본 관계 리셋
TARGET = 0
ASSI = 0
PBAND = 12
;패치에서 추가 부분
	ITEM:101 = 0
	ITEM:102 = 0
	ITEM:103 = 0
	ITEM:104 = 0
	ITEM:105 = 0
;Default 옵션
CALL DEFAULT_OPTION
;초기 지점 설정
FOR LOCAL, 0, CHARANUM
	;개시 위치
	CFLAG:LOCAL:현재위치 = CFLAG:LOCAL:초기위치
	;복장
	CALL SELECT_CLOTHES(LOCAL, "오늘의 의상")
	CALL CTRL_CLOTHES_SET(LOCAL, "현재 의상의 변경_평상복")
	CALL 속옷확인리셋(LOCAL)
	;이것 아마 문제 안되겠지만 일단
	CFLAG:LOCAL:파자마 = 0
	CALL CLOTHES_SETTING_TRAIN(LOCAL)
NEXT

;MASTER 기상시간 세트(7시)
TIME:3 = 420

;MASTER 수면중 플래그 ON
CALL SET_SLEEP(1, MASTER, 0)

;일자 1 일째
DAY = 1
DAY:2 = 1
DAY:3 = 1

BASE:MASTER:체력 = MAXBASE:MASTER:체력
BASE:MASTER:기력 = MAXBASE:MASTER:기력
BASE:MASTER:정력 = MAXBASE:MASTER:정력


CFLAG:MASTER:취침시간 = 0
CALL SET_NOUMIN
CALL SET_MAINHOME
STR:16 = 낡은오두막
FLAG:술벌레 = 1

;당신의 기정사실 및[연인]을 클리어
CLEARBIT CFLAG:MASTER:기정사실, 2
TALENT:MASTER:연인 = 0

;다 되고 있는 캐릭터의 출금을 체크
SIF FLAG:역극
	CFLAG:(FLAG:역극):출금 = LOCAL:5

CALL VIRGIN_SET
CALL 특전
CALL SET_CHARISMA("RESET")

PRINTFORML %CALLNAME:MASTER%의 경험·능력도 리셋하겠습니까?
CALL COLORMESSAGE(@"실적으로 취득한 소질 등도 취소되기 때문에 추천하지 않습니다", C_YELLOW, 1)
CALL ASK_YN
IF !RESULT
	FOR LOCAL, 0, 150
		EXP:MASTER:LOCAL = 0
		SIF LOCAL < 100
			ABL:MASTER:LOCAL = 0
	NEXT
	PRINTFORMW 리셋 했습니다.
ENDIF

PRINTFORML %마스터를% 커스터마이즈하겠습니까?
CALL ASK_YN
IF !RESULT
	CALL TINKO_DECIDE(MASTER)
	CALL CUSTOM_TERMINAL(MASTER)
ENDIF

SIF LOCAL
	WAIT
;SYSTEMCALL to SHOW_SHOP
BEGIN SHOP

@특전
PRINTW *연인 특전*
PRINTW 이전에 연인이 된 적이 있는 캐릭터의 소질을 변경할 수 있습니다
$LOOP
DRAWLINE
FOR LOCAL, 1, CHARANUM
SIF GETBIT (CFLAG:LOCAL:인계, 1)
	PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL%
NEXT
PRINTL
DRAWLINE
PRINTL [999] 다음
INPUT
IF RESULT >= 1 && RESULT <= CHARANUM && GETBIT (CFLAG:RESULT:인계, 1)
	CALL CUSTOM_TERMINAL(RESULT)
	GOTO LOOP
ENDIF

PRINTW *연모 특전*
PRINTW 이전에 연모를 취득한 적이 있는 캐릭터의 일부 소질을 변경할 수 있습니다
$LOOP2
DRAWLINE
FOR LOCAL, 1, CHARANUM
SIF GETBIT (CFLAG:LOCAL:인계, 0)
	PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL%
NEXT
PRINTL
DRAWLINE
PRINTL [999] 다음
INPUT
IF RESULT >= 1 && RESULT <= CHARANUM && GETBIT (CFLAG:RESULT:인계, 0)
	CALL LIMITED_CUSTOM(RESULT)
	GOTO LOOP2
ENDIF
FOR LOCAL, 1, CHARANUM
	SIF TCVAR:LOCAL:감도상승부위
		TALENT:LOCAL:(TCVAR:LOCAL:감도상승부위) ++
NEXT

@SET_인계여부
#DIM CHARA_ID
PRINTFORML 신부(전 신부)의 경우 인계할 수 있습니다.
PRINTFORML 인계할 캐릭터를 선택해주세요.
DRAWLINE
FOR CHARA_ID, 1, CHARANUM
	IF !GETBIT(TFLAG:(CHARA_ID + 500),7)
		;;인계하는 캐릭터에게 따로 처리할 부분
		CONTINUE
	ENDIF
	SIF GETBIT(CFLAG:CHARA_ID:인계,8)
		SETCOLOR C_PINK
	PRINTFORMLC [{CHARA_ID, 3}] %CALLNAME:CHARA_ID%
	RESETCOLOR
NEXT
PRINTL 　
DRAWLINE
PRINTL [999] 설정 종료
DO
	INPUT
	CHARA_ID = RESULT
	SELECTCASE CHARA_ID
	CASE 999
		PRINTW 개별 설정을 종료합니다.
		RETURN
	CASE 1 TO CHARANUM
		BREAK
	CASEELSE
	ENDSELECT
LOOP 1

IF !GETBIT(CFLAG:CHARA_ID:인계,8)
	PRINTFORMW %CALLNAME:CHARA_ID%를 인계합니다.
	SETBIT CFLAG:CHARA_ID:인계,8
	
ELSE
	PRINTFORMW %CALLNAME:CHARA_ID%를 인계하지 않습니다.
	CLEARBIT CFLAG:CHARA_ID:인계,8
ENDIF
DRAWLINE
RESTART