;--------------------------------------------------
;	MAP_MANAGE.ERB
;	이사 패치 관리
;				공개 개시：2015/09/14
;				최종 갱신：2015/10/17
;--------------------------------------------------
;
;실장이 끝난 상태：
;	"[400]이동"시 맵 표시
;	"[400]이동"시 이동 불능 판정
;	MASTER 원거리이동
;	지점 관련성 응답 처리
;	지명 캐릭터라인 취득
;	목욕탕, 청소, 공부, 훈련의 커맨드 표시 판정
;	CHARA 귀가 관련
;	CHARA 목욕탕 관련
;	일, 연회 관계
;미실장：
;	기색 짐작의 실장
;	그 외
;------------------------------------------
;"이동하는"으로 표시되는 맵 화면을 표시시킨다
;이사 계획에서는 MAIN_MAP 이외는 ODEKAKEMAP를 사용할 예정이었다
;요점 참조 COMF400_이동. ERB
;------------------------------------------
@MAP_VIEWING
TRYCALLFORM MAP_VIEWING_{MAIN_MAP}

;------------------------------------------
;기색 표시를 자택인가 그렇지 않은 것처럼 따라 처리를 바꿀 수 있도록(듯이) 한 GETMAP
;요점 참조 COMF400_이동. ERB, 호출원
;요점 참조 MAP_0.ERB
;ARG, 장소 ID
;------------------------------------------
@GETMAP(ARG)
IF MAIN_MAP == GET_MAPID(ARG)
	TRYCALLFORM GETMAP_{MAIN_MAP}(ARG)
ELSE
ENDIF
;------------------------------------------
;사이에 얼마든지 CALL GETMAP를 사이에 둔 맵을 써 일으키는 것이 귀찮았기 때문에 만든 함수
;------------------------------------------
@PRINTMAP(ARGS, ARG, ARGS:1, ARG:1, ARGS:2, ARG:2, ARGS:3, ARG:3, ARGS:4)
#DIM 지점수

FOR 지점수, 0,5
	PRINTFORM %ARGS:지점수%
	IF !ARG:지점수
		PRINTL
		BREAK
	ENDIF
	CALL GETMAP(ARG:지점수)
NEXT

;------------------------------------------
;초대방의 이름 캐릭터라인을 돌려준다. 초대되기 전은 TARGET의 초대방캐릭터라인을 돌려준다
;호출원:NAME_FROM_PLACE()
;ARG, VOID
;------------------------------------------
@OMANEKI_PLACE(ARG)
#FUNCTIONS
#DIMS PNAME
#DIM OMANEKI_TARGET
IF CFLAG:MASTER:초대
	OMANEKI_TARGET = CFLAG:MASTER:초대
ELSE
	OMANEKI_TARGET = TARGET
ENDIF
SIF ARG
	OMANEKI_TARGET = ARG

PNAME = %CALLNAME:(OMANEKI_TARGET)%

IF TEQUIP:목욕탕플레이
	PNAME = %PNAME%의 욕실
ELSE
	SELECTCASE OMANEKI_TARGET
		CASE 80;히에다노
				PNAME = 히에다 저택
		CASEELSE
			IF OUTROOF(CFLAG:OMANEKI_TARGET:자택위치)
				PNAME = %PNAME%의 거주지
			ELSE
				PNAME = %PNAME%의 방
			ENDIF
	ENDSELECT
ENDIF
RETURNF PNAME

;------------------------------------------
;참배길(출입구)의 장소 ID를 돌려준다
;------------------------------------------
@GET_ENTRANCE(MAPID)
#FUNCTION
#DIM MAPID
;	//ODEKAKEMAP 사용시
SIF MAPID != MAIN_MAP
	RETURNF MAPID * 100

IF GET_EXISTMAP(MAPID)
	RETURNF ENTRANCE
ELSE
	RETURNF MAPID * 100
ENDIF
;------------------------------------------
;장소 ID가 MAPID의 참배길(출입구)이나 판정한다
;MAPID를 생략 했을 경우는 MAPID에 ARG의 소속 MAP를 대입한다
;ARG:장소 ID
;MAPID：대상 MAP
;------------------------------------------
@CHK_ENTRANCE(ARG, MAPID = 999)
#FUNCTION
#DIM MAPID
;	// MAPID가 생략 되었을 경우
SIF MAPID == 999
	MAPID = GET_MAPID(ARG)
;	//ODEKAKEMAP 사용시
SIF ARG == GET_ENTRANCE(MAPID)
	RETURNF 1
;출발 전용의 현관(뒷문), 무엇인가를 위해서(때문에) 구별할 수 있도록(듯이) 2로 해 둔다
SIF MAPID == 7 && ARG == 732
	RETURNF 2
RETURNF 0
;------------------------------------------
;	이사의 표시와 처리
;------------------------------------------
@SET_MAINHOME
;
;멤버의 기상시간재설정
;
PRINTL 자택을 설정할 수 있습니다.
PRINTL 어디로 설정하겠습니까?
;SETCOLOR C_YELLOW
;	PRINTFORML 요괴의 산·산정은 막 실장했기에 오류가 발생할 확률이 높습니다
RESETCOLOR
FOR LOCAL, 0,15
	SIF GET_EXISTMAP(LOCAL)
		PRINTFORML [{LOCAL}] %GET_MAPNAME(LOCAL)%
NEXT
IF DAY
	DRAWLINE
	PRINTL [100]캔슬
ENDIF
$LOOP
INPUT
LOCAL:1 = RESULT
IF GET_EXISTMAP(RESULT)
	FLAG:운세뽑기계약 = 0
	CALL SET_ENKAI_LOCATIONDISPLAY
	FOR LOCAL, 1, CHARANUM
		;내방시간
		CFLAG:LOCAL:내방시간 = CSVCFLAG(LOCAL, 352)
		;귀가시간
		CFLAG:LOCAL:귀가시간 = CSVCFLAG(LOCAL, 353)
		;취침시간
		CFLAG:LOCAL:취침시간 = CSVCFLAG(LOCAL, 354)
		;기상시간
		CFLAG:LOCAL:기상시간 = CSVCFLAG(LOCAL, 355)
		
		CFLAG:LOCAL:신사거주 = 0
		
		CFLAG:LOCAL:초기위치 = CSVCFLAG (LOCAL, 311)
		CFLAG:LOCAL:자택위치 = CSVCFLAG (LOCAL, 358)
	NEXT
	CALL MAP_DEFAULTRESIDENT(LOCAL:1)
		;자물쇠를 채워 문을 잠금
		VARSET LOCK

	FOR LOCAL, 1, CHARANUM
		CALL CHARA_SLEEP, LOCAL, 0
	NEXT
ELSEIF RESULT == 100
ELSE
	GOTO LOOP
ENDIF
SIF FLAG:역극 && CFLAG:(FLAG:역극):출금 && MAIN_MAP == GET_MAPID(CSVCFLAG(FLAG:역극, 311))
	CFLAG:MASTER:초기위치 = CSVCFLAG(FLAG:역극, 311)

@HAS_DUPLICATE_KEY, ARG
#FUNCTION
	FOR LOCAL, 1, CHARANUM
		IF CFLAG:LOCAL:311 == ARG
			SIF TALENT:LOCAL:연모
				RETURNF 1
		ENDIF
	NEXT

@MAP_DEFAULTRESIDENT(ARG)
;ARG=맵 번호
FLAG:더부살이캐릭터 = 0
MAIN_MAP = ARG
FOR LOCAL, 1, CHARANUM
	IF MAP_거주자(MAIN_MAP, LOCAL)
		;PRINTFORML %CALLNAME:LOCAL% {CFLAG:LOCAL:저택에거주}맵{MAIN_MAP}
		IF (MAIN_MAP==13||MAIN_MAP==14)&& CFLAG:LOCAL:저택에거주>0
			CFLAG:LOCAL:초기위치=CFLAG:LOCAL:저택에거주
			CFLAG:LOCAL:신사거주 = CFLAG:LOCAL:초기위치
		ELSE
			CFLAG:LOCAL:초기위치 = CSVCFLAG(LOCAL, 311)
			CFLAG:LOCAL:신사거주 = CFLAG:LOCAL:초기위치
			;PRINTFORML %CALLNAME:LOCAL%가 거점에 삽니다
			IF LOCAL == 57
				CFLAG:RESULT:신사거주 = 55
				CFLAG:RESULT:초기위치 = 55
			ELSEIF  LOCAL == 71
				CFLAG:71:신사거주 = 54
				CFLAG:71:초기위치 = 54
			ENDIF
		ENDIF
	ELSE
		PRIVATEROOM:(CFLAG:LOCAL:초기위치 % 100) = 0
		CFLAG:LOCAL:초기위치 = SUKIMA()
		CFLAG:LOCAL:신사거주 = 0
	ENDIF
	CFLAG:LOCAL:현재위치 = CFLAG:LOCAL:초기위치
	CFLAG:LOCAL:데이트중 = MAIN_MAP
NEXT
PRINTFORMW %GET_MAPNAME(ARG)%로 설정했습니다
CALL ROOMSETTING
CFLAG:MASTER:초기위치 = Default초기위치
CFLAG:MASTER:현재위치 = CFLAG:MASTER:초기위치

@ROOMSETTING
#DIM R_ID
LOCAL:1 = 0
SUMIKOMI_ROOM = 0
TRYCCALLFORM ROOMSETTING_{MAIN_MAP}
	PRINTFORMW %GET_MAPNAME(MAIN_MAP)%의 방 정보를 설정했습니다
	IF FLAG:더부살이캐릭터 && SUMIKOMI_ROOM
		R_ID = SUMIKOMI_ROOM % 100
		ROOMDATA:R_ID |= 장소_이불
		ROOMDATA:R_ID |= 장소_옥내
		ROOMDATA:R_ID |= 장소_선잠_가능
		ROOMDATA:R_ID |= 장소_Private
		ROOMDATA:R_ID &= ~장소_과소
	ENDIF
CATCH
	PRINTFORMW 방 정보 설정에 실패했습니다
ENDCATCH
FOR LOCAL, 1, CHARANUM
	IF MAP_거주자(MAIN_MAP, LOCAL) && !CFLAG:LOCAL:출금
		;PRIVATEROOM 변수에, 거기의 집주인의 번호를 보존
		PRIVATEROOM:(CFLAG:LOCAL:초기위치 % 100) = LOCAL 
	 	SIF CFLAG:LOCAL:집주인후보
			LOCAL:1 ++
	ENDIF
NEXT

IF !Default초기위치 || Default초기위치 / 100 != MAIN_MAP
	SIF !LOCAL:1||MAIN_MAP>=13
		GOTO SONOMAMA
	
	PRINTFORMW 이 맵에는 방 후보가 다수 존재합니다.
	SIF DAY <= 1
		PRINTFORMW 첫날 한정으로, 필요 신뢰도에 관계없이 방을 선택할 수 있습니다.
	PRINTFORML 
	FOR LOCAL, 1, CHARANUM
		IF MAP_거주자(MAIN_MAP, LOCAL) && CFLAG:LOCAL:집주인후보
			LOCAL:1 ++
			SIF CFLAG:LOCAL:더부살이필요신뢰도 > CFLAG:LOCAL:신뢰도 && DAY > 1
				SETCOLOR C_L_GRAY
			IF MAIN_MAP == 0
				LOCALS = %STR:(CFLAG:LOCAL:집주인후보)%
			ELSE
				LOCALS = %STR:(CFLAG:LOCAL:집주인후보 + 8000)%
			ENDIF
			PRINTFORML [{LOCAL}] - %LOCALS%　집주인 :%CALLNAME:LOCAL% 필요 신뢰도:{CFLAG:LOCAL:더부살이필요신뢰도}
			RESETCOLOR
		ENDIF
	NEXT
	
	PRINTFORML [0] - 그대로
	$LOOP
	INPUT
	IF MAP_거주자(MAIN_MAP, RESULT) && CFLAG:RESULT:집주인후보
		IF CFLAG:RESULT:더부살이필요신뢰도 > CFLAG:RESULT:신뢰도 && DAY > 1
			PRINTFORMW %CALLNAME:RESULT%는 그만큼 %마스터를% 신뢰하고 있지 않다
			GOTO LOOP
		ENDIF
		거점_주인 = RESULT
		Default초기위치 = CFLAG:RESULT:집주인후보
		ROOMDATA:(Default초기위치 % 100) |= 장소_이불
		PRINTFORMW %STR:(CFLAG:RESULT:집주인후보 + (MAIN_MAP ?  8000 # 0) )%로 이사합니다
	ELSEIF !RESULT
		$SONOMAMA
		TRYCCALLFORM DEFAULT_OWNER_{MAIN_MAP}
		CATCH
			PRINTFORMW 방 정보 설정에 실패했습니다
		ENDCATCH
			IF MAIN_MAP == 0
				LOCALS = %STR:(Default초기위치)%
			ELSE
				LOCALS = %STR:(Default초기위치 + 8000)%
			ENDIF
			PRINTFORMW %LOCALS%로 이사합니다
	ELSE
		GOTO LOOP
	ENDIF

ENDIF
