@EVENTTURNEND
#DIM 양육비
;SIF TFLAG:망상중
;	DELCHARA (CHARANUM - 1)
;연회 당일에 하루가 끝나면 연회 플래그 OFF
;IF DAY >= FLAG:34 && FLAG:연회개최플래그
IF TIME + 1440 * DAY >= FLAG:개시시간 + 1440 * FLAG:개시일 && FLAG:연회개최플래그
	;회장이 된 장소는 몹시 황폐해지는, 거점외에서도 대상 지점의 더러운 증가시키기 (위해)때문에 연회장은 아니다
	SIF YOGORE:(FLAG:32) < 300
		YOGORE:(FLAG:32) += 1000
	FLAG:연회개최플래그 = 0
	FLAG:31 = 0
	FLAG:32 = 0
	TFLAG:연회장 = 0
	FLAG:33 = 0
	FLAG:34 = 0
	FLAG:35 = 0
	FLAG:36 = 0
	;연회회장표시 = 
	FLAG:연회차입 = 0
	FOR LOCAL, 0, CHARANUM
		CFLAG:LOCAL:350 = 0
		MAXBASE:LOCAL:일량 = 0
		BASE:LOCAL:일량 = 0
		CFLAG:LOCAL:연회참가 = 0
		CFLAG:LOCAL:행동 = 0
	NEXT
ENDIF
;사당 플래그 OFF
FLAG:17 = 0
DRAWLINE
PRINTFORML 
;SETCOLOR C_PINK
;FONTBOLD
FOR LOCAL, 1, CHARANUM
	;연모 취득
	IF 연모취득조건(LOCAL)
		;면영기는 특별 취급
		IF LOCAL == 57
			PRINTFORMW %CALLNAME:LOCAL%는 %CALLNAME:MASTER%에의 마음을 자각함과 동시에, 입술끝에 미소를 머금었다
			PRINTFORMW 자연스러운 웃는 얼굴을 띠는 %CALLNAME:LOCAL%는 표정을 얻은 같다
			PRINTFORMW %CALLNAME:LOCAL%의 \@ TALENT:LOCAL:사모 ? [사모]는 # [애욕]은 \@ [연모]가 되었다
			FLAG:10 ++
		;애욕의 유무로 분기
		ELSEIF TALENT:LOCAL:애욕
			PRINTFORMW %CALLNAME:LOCAL%는 몸도 마음도 %CALLNAME:MASTER%에 채워지는 것에 즐거움을 느끼고 있다…
			PRINTL 문란한 관계 끝에 여문 연정, 그것도 또 하나의 사랑의 형태…
			PRINTFORMW %CALLNAME:LOCAL%의 [애욕]은 [연모]가 되었다
		ELSE
			PRINTFORML %CALLNAME:LOCAL%는 %마스터와% 보내는 시간에 행복을 느끼고 있는 것 같다…
			PRINTFORMW %CALLNAME:LOCAL%의 [사모]는 [연모]가 되었다
			FLAG:10 ++
		ENDIF
		SIF !MARK:LOCAL:시간각인 && !TALENT:LOCAL:애욕
			CFLAG:LOCAL:깨끗한교제 = 1
		TALENT:LOCAL:연모 = 1
		TALENT:LOCAL:사모 = 0
		TALENT:LOCAL:애욕 = 0
		TALENT:LOCAL:섹프 = 0
		CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 1,0)
	;사모 취득
	;섹프/정부의 취득으로 분기
	ELSEIF 사모취득조건(LOCAL)
		IF TALENT:LOCAL:섹프
			PRINTFORML %CALLNAME:LOCAL%는 %CALLNAME:MASTER%의 몸 외에 대해서도 흥미가 있는 것 같다
			PRINTFORMW %CALLNAME:LOCAL%는 [사모]를 얻었다
		ELSE
			PRINTFORML %CALLNAME:LOCAL%는 %CALLNAME:MASTER%에 대한 것이 신경이 쓰여요 다…
			PRINTFORMW %CALLNAME:LOCAL%는 [사모]를 얻었다
		ENDIF
		TALENT:LOCAL:사모 = 1
		CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 2,0)
	;애욕 취득
	;봉사 쾌락의 분기가 아직 없기 때문에, 취득 조건에 병합
	ELSEIF 애욕취득조건(LOCAL)
		;사모 가져, 진심으로 몸을 선택한적인
		IF TALENT:LOCAL:사모
			PRINTFORML %CALLNAME:LOCAL%는 %마스터와% 마음 이상으로 몸의 연결에 만족한 것 같다…
			PRINTFORML 눈에 보이지 않는 정보다 눈에 보이는 증거를 바라 앞으로도 몸을 겹치는 것일까
			PRINTFORMW %CALLNAME:LOCAL%는 [애욕]을 얻었다
		ELSE
			PRINTFORML %CALLNAME:LOCAL%는 %마스터와%의 정욕에 빠지고 있는 것 같다…
			PRINTFORMW %CALLNAME:LOCAL%는 [애욕]을 얻었다
		ENDIF
			FLAG:10 ++
			TALENT:LOCAL:애욕 = 1
			TALENT:LOCAL:사모 = 0
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 3,0)
	;섹프/정부 취득
	;섹프와 정부의 차이는 사모의 유무
	ELSEIF 섹프취득조건(LOCAL)
		;사모 취득제＋섹프미취득
		IF TALENT:LOCAL:사모 && !TALENT:LOCAL:섹프
			PRINTFORML %CALLNAME:LOCAL%는 %마스터와% 함께라면 쾌락에 빠져도 괜찮다고 생각하기 시작한 것 같다…
			PRINTFORMW %CALLNAME:LOCAL%는 %CALLNAME:MASTER%의 [정부]가 되었다
			TALENT:LOCAL:섹프 = 2
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 4,0)
		;사모 취득제＋섹프 취득제
		ELSEIF TALENT:LOCAL:사모 && TALENT:LOCAL:섹프 == 1
			PRINTFORML %CALLNAME:LOCAL%는 %CALLNAME:MASTER%에게 단순한 육체 관계 이상의 것을 바라고 있는 것 같다…
			PRINTFORMW %CALLNAME:LOCAL%는 [정부]가 되었다
			TALENT:LOCAL:섹프 = 2
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 4,0)
		;사모미취득＋섹프미취득이라면 우선 섹프 취득
		ELSEIF !TALENT:LOCAL:섹프
			PRINTFORML %CALLNAME:LOCAL%는 %마스터가% 주는 쾌락에 빠져 있다…
			PRINTFORMW %CALLNAME:LOCAL%는 %CALLNAME:MASTER%의 [섹프]가 되어버렸다
			TALENT:LOCAL:섹프 = 1
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 5,0)
		ENDIF
	ENDIF

	IF EXP:LOCAL:키스경험 > 300 && ABL:LOCAL:M감각 > 5 && !TALENT:LOCAL:키스마
		PRINTFORMW %CALLNAME:LOCAL%는 [키스마]를 얻었다
		TALENT:LOCAL:키스마 = 1
	ENDIF
	IF EXP:LOCAL:조교자위경험 > 200 && EXP:LOCAL:자위경험 > 500 && !TALENT:LOCAL:자위중독
		PRINTFORMW %CALLNAME:LOCAL%는 [자위중독]을 얻었다
		TALENT:LOCAL:자위중독 = 1
	ENDIF
	IF EXP:LOCAL:자궁구개발경험 > 50 && !TALENT:LOCAL:포르치오성감
		PRINTFORML 반복해 자궁구를 자극받은 %CALLNAME:LOCAL%는 고통보다 쾌락을 느끼게 된 것 같다…
		PRINTFORMW %CALLNAME:LOCAL%는 [포르치오성감]을 얻었다
		TALENT:LOCAL:포르치오성감 = 1
	ENDIF
	IF EXP:LOCAL:연주경험 >= 1000 && ABL:LOCAL:기교 > 3 && !TALENT:LOCAL:음악지식
		PRINTFORMW %CALLNAME:LOCAL%는 악기 연주의 조예가 깊어졌다
		PRINTFORMW %CALLNAME:LOCAL%는 [악기지식]을 얻었다
		;PRINTFORMW %CALLNAME:LOCAL%는 음악의 조예가 깊어졌다
		;PRINTFORMW %CALLNAME:LOCAL%는 [음악지식]을 얻었다
		TALENT:LOCAL:음악지식 = 1
	ELSEIF EXP:LOCAL:연주경험 >=5000 && !TALENT:LOCAL:음감
		PRINTFORMW %CALLNAME:LOCAL%의 음감은 꽤 단련할 수 있어 온 것 같다
		PRINTFORMW %CALLNAME:LOCAL%는 [상대 음감]을 얻었다
		TALENT:LOCAL:음감 = 1
	ENDIF
	IF CFLAG:LOCAL:언짢음
		CFLAG:LOCAL:언짢음 --
		IF !CFLAG:LOCAL:언짢음
			SIF MARK:LOCAL:반발각인
				CFLAG:LOCAL:언짢음 ++
		ENDIF
	ENDIF
	;EX캐릭터의 연모시는 멤버 추가 이벤트 플래그를 세운다
	IF LOCAL >= 11 && LOCAL <= CHARANUM && LOCAL != 57 && LOCAL != 71
		SIF TALENT:LOCAL:연모 && EXP:LOCAL:애정경험 >= 200
			FLAG:1 = 3
	ENDIF
NEXT

IF EXP:MASTER:이상청소경험 > (500 + 100 * FLAG:850) && ABL:MASTER:청소기능 > 3 && !TALENT:MASTER:청소중독
	PRINTFORMW %마스터는% 무서운 것에 깨닫아 버렸다
	PRINTFORMW 항상, 시간을 멈추고 있는 동안도, 소녀들과 살을 맞대고 있을 때조차,
	PRINTFORMW 방이 어질러져 있지 않은지 신경 쓰여 못 견디는 것이다
	PRINTFORMW 이미 %CALLNAME:MASTER%의 마음이 편안해지는 것은 이렇게 해 청소를 할 때뿐
	PRINTFORMW %CALLNAME:MASTER%의 청소에의 조건은, 돌이킬 수 없는 경지에 이르고 있었다
	PRINTFORMW %마스터는% [청소중독]을 얻었다
	TALENT:MASTER:청소중독 = 1
ENDIF
;MASTER를 제외한 전원의 pre-set 처리
FOR LOCAL, 1, CHARANUM
	;기력의 회복
	BASE:LOCAL:기력 = MAXBASE:LOCAL:기력

	LOCAL:1 = MAXBASE:LOCAL:체력 / 3

	;회복 빠름, 회복 느림
	LOCAL:1 = LOCAL:1 * 3 / (3 - TALENT:LOCAL:회복속도)
	
	;동침하고 있으면 회복속도 향상
	SIF CFLAG:LOCAL:동침중
		LOCAL:1 *= 2
	SIF CFLAG:LOCAL:철야
		LOCAL:1 /= 2
	CFLAG:LOCAL:철야 = 0
	BASE:LOCAL:체력 = MIN(MAXBASE:LOCAL:체력, BASE:LOCAL:체력 + LOCAL:1)

	;나무 삼개 드 발행자가 있으면 발행 플래그 OFF
	CFLAG:LOCAL:328 = 0
NEXT
;전원의 pre-set 처리
FOR LOCAL, 1, CHARANUM
	BASE:LOCAL:정력 = MIN(MAXBASE:LOCAL:정력, BASE:LOCAL:정력 + MAXBASE:LOCAL:정력 * BASE:LOCAL:체력 / MAXBASE:LOCAL:체력)
	
	;은밀중의 캐릭터가 있으면 플래그 OFF
	CALL SET_SNEAK(0, LOCAL, 1)
	;CFLAG:LOCAL:은밀중 = 0
	
	;장난중의 캐릭터가 있으면 플래그 OFF
	CALL SET_PRANK(0, LOCAL, 1)
	;CFLAG:LOCAL:장난 = 0
	
NEXT

;연회의 개최 판정
SIF FLAG:3 && FLAG:연회개최플래그 == 0
	CALL ENKAI_SETTING

IF CFLAG:MASTER:육아인원수
	양육비 = CFLAG:MASTER:육아인원수 * 300 / (1 + FLAG:생활능력무)
	PRINTFORML %마스터는% 아이의 양육비로서 \\{양육비} 지불했다.
	MONEY -= 양육비
ENDIF

CALL RANK_POINT
PRINTW  

FLAG:0 = 0


BEGIN SHOP

@RANK_POINT
VARSET LOCAL
;청결도
LOCAL:2 = EX:MASTER:오늘의청소회수 / 10
PRINTFORML 오늘 청소한 쓰레기의 양 {EX:MASTER:오늘의청소회수}
PRINTFORML 청소로 얻은 보너스 \\{LOCAL:2}
IF DAY:2 == 4 && DAY:3 == 31
	PRINT (x2)  【대청소 보너스】
	LOCAL:2 *= 2
ENDIF
;거점의 주인의 호감도와 신뢰도가 상승
LOCAL:3 = MIN(10, EX:MASTER:오늘의청소회수 / 2000)
IF LOCAL:3
	PRINTFORML 청소를 열심히 한 덕분에 %CALLNAME:거점_주인%로부터의 평가가 올랐다
	CFLAG:거점_주인:호감도 += LOCAL:3 * 2
	CFLAG:거점_주인:신뢰도 += LOCAL:3
ENDIF
PRINTL  
;심부름 보수
LOCAL:4 = TCVAR:MASTER:도운양

SIF LOCAL:4 > 0
	PRINTFORML 업무 도우미로 얻은 보너스 \\{LOCAL:4}

MONEY += LOCAL:2 + LOCAL:4

;호감도 상승
IF FLAG:12 && MONEY:2 < CM_LIMIT
	LOCAL:5 = MAX(1, FLAG:12 / 100)
	LOCAL:5 = MIN(LOCAL:5, CM_LIMIT - MONEY:2)
	PRINTFORML 호감도 상승 보너스 ＋{LOCAL:5}카리스마
	MONEY:2 += LOCAL:5
	PRINTFORML 현재의 카리스마：{MONEY:2}　카리스마 보유 상한：{CM_LIMIT}
ENDIF
FLAG:12 = 0

PRINTW  

@연모취득조건(ARG)
#FUNCTION
SIF TALENT:ARG:연모
	RETURNF 0
SIF CFLAG:ARG:호감도 < REQUIRED_FAVOR_연모
	RETURNF 0
SIF CFLAG:ARG:신뢰도 < REQUIRED_TRUST_연모
	RETURNF 0
SIF ABL:ARG:친밀 < REQUIRED_AFFECTION_연모
	RETURNF 0
SIF MARK:ARG:반발각인
	RETURNF 0
SIF TALENT:ARG:애욕 && !FLAG:함락노선변화
	RETURNF 0
RETURNF 1

@사모취득조건(ARG)
#FUNCTION
SIF TALENT:ARG:애욕
	RETURNF 0
SIF TALENT:ARG:연모
	RETURNF 0
SIF TALENT:ARG:사모
	RETURNF 0
SIF CFLAG:ARG:호감도 < REQUIRED_FAVOR_사모
	RETURNF 0
SIF CFLAG:ARG:신뢰도 < REQUIRED_TRUST_사모
	RETURNF 0
SIF ABL:ARG:친밀 < REQUIRED_AFFECTION_사모
	RETURNF 0
SIF TALENT:ARG:섹프 && !FLAG:함락노선변화
	RETURNF 0
RETURNF 1

@애욕취득조건(ARG)
#FUNCTION
SIF TALENT:ARG:연모
	RETURNF 0
SIF TALENT:ARG:애욕
	RETURNF 0
SIF !TALENT:ARG:섹프
	RETURNF 0
SIF CFLAG:ARG:호감도 < REQUIRED_FAVOR_애욕
	RETURNF 0
SIF ABL:ARG:욕망 < REQUIRED_DESIRE_애욕
	RETURNF 0
SIF (EXP:ARG:절정경험 - EXP:ARG:무자각절정경험) < REQUIRED_ECSTACY_애욕
	RETURNF 0
SIF (ABL:ARG:욕망 - ABL:ARG:친밀) < 1
	RETURNF 0
RETURNF 1

@섹프취득조건(ARG)
#FUNCTION
SIF TALENT:ARG:연인
	RETURNF 0
SIF TALENT:ARG:연모
	RETURNF 0
SIF TALENT:ARG:섹프 == 2
	RETURNF 0
SIF CFLAG:ARG:호감도 <= REQUIRED_FAVOR_섹프
	RETURNF 0
SIF ABL:ARG:욕망 < REQUIRED_DESIRE_섹프
	RETURNF 0
SIF (EXP:ARG:절정경험 - EXP:ARG:무자각절정경험) < REQUIRED_ECSTACY_섹프
	RETURNF 0
SIF (ABL:ARG:욕망 - ABL:ARG:친밀) < 0
	RETURNF 0
RETURNF 1
