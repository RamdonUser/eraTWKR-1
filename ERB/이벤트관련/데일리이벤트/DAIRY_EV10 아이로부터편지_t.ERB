@DAIRY_CONDITION_EV10
#DIM YOME
#DIM CHILDNUM
SIF !CFLAG:MASTER:아이인원수
	RETURN -1
SIF RAND:20 > 0
	RETURN -1
YOME = 0
CALL FISHER_YATES_SHAFFLE(CHARANUM - 1)
FOR LOCAL:10, 0, CHARANUM - 1
	LOCAL = SHAFFLE_ARRAY:(LOCAL:10) + 1
	IF CFLAG:LOCAL:아이인원수 - CFLAG:LOCAL:육아인원수 > 0
		YOME = LOCAL
		CHILDNUM = RAND:(CFLAG:LOCAL:아이인원수 - CFLAG:LOCAL:육아인원수) + 1
		BREAK
	ENDIF
NEXT
IF YOME
	CALL DAIRY_EV10(YOME, CHILDNUM)
	RETURN 1
ELSE
	RETURN -1
ENDIF


@DAIRY_EV10(ARG, ARG:1)
;PRINTFORMW %CALLNAME:ARG%과의 아이의 %CHILDNAME:ARG:(ARG:1)%로부터 편지가 왔다
PRINTFORMW %조사처리(CHILDNAME:ARG:(ARG:1),"로")%부터 편지가 왔다
LOCAL:1 = 0
FOR LOCAL, 1, CHARANUM
	SIF CFLAG:LOCAL:호감도 > CFLAG:ARG:호감도
		LOCAL:1 ++
NEXT

TARGET = ARG
;말전용의 편지 이벤트 분기(없으면 통상 이벤트)
IF !RAND:2
	TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_DAIRY_말편지(ARG:1)
	CATCH
		GOTO TEGAMI
	ENDCATCH
ELSE
	$TEGAMI
	IF LOCAL:1 >= 3
		TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_DAIRY_공통편지(ARG:1, "바람둥이")
		CATCH
			PRINTFORMW 좀 더 %조사처리(CALLNAME:ARG,"를")% 소중히 하라고 적혀있었다……
		ENDCATCH
	ELSEIF !RAND:5
		TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_DAIRY_공통편지(ARG:1, "용돈주기") ;金の無心
		;金銭を無遠慮にねだって得ようとすること、思慮なく金をせびることを意味する語。親に小遣いをたかることを指す場合が多い。 
		;용돈 달라는 뜻
		CATCH
			PRINTFORMW 용돈을 달라고 한다……
			;金の無心だ……
		ENDCATCH
		PRINTFORMW %마스터는% \@ MONEY > 30000? ￥30000을# 가진 돈 전부를\@ 보내기로 했다
		MONEY = MAX(MONEY - 30000,0)
	ELSE
		TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_DAIRY_공통편지(ARG:1, "송금")
		CATCH
			PRINTFORMW %조사처리(CALLNAME:ARG,"와")% %마스터를% 신경쓰는 말과 송금\\10000이 들어가 있었다
		ENDCATCH
		MONEY += 10000
	ENDIF
ENDIF
TARGET = TARGET:1
RETURN 1