;----------------------------------------------------------------------------------------------------
;의뢰 수탁 처리
;----------------------------------------------------------------------------------------------------
@SET_IRAI_SLOT(CHARA)
#DIM CHARA
#DIM IRAI_ID
#DIM CHK_SLOT
IRAI_ID = CFLAG:CHARA:의뢰내용
CHK_SLOT = SEARCH_IRAI_EMPTY()
SIF !CHK_SLOT
	RETURN
IRAI_SLOT:CHK_SLOT     = IRAI_ID
IRAI_DATA:CHK_SLOT     = INT_DATA_IRAI(IRAI_ID, "의뢰내용",     CHARA)
IRAI_SUB:CHK_SLOT      = INT_DATA_IRAI(IRAI_ID, "의뢰 서브",     CHARA)
IRAI_DEADLINE:CHK_SLOT = INT_DATA_IRAI(IRAI_ID, "의뢰기한", CHARA) + DAY
CALL SET_IRAI_PROGRESS(CHARA, "의뢰중")



;----------------------------------------------------------------------------------------------------
;의뢰 종료 처리
;----------------------------------------------------------------------------------------------------
@CLEAR_IRAI_SLOT(CHARA, CHK_SLOT, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIM  CHK_SLOT
#DIMS SCENE
ARRAYREMOVE IRAI_SLOT,     CHK_SLOT, 1
ARRAYREMOVE IRAI_DATA,     CHK_SLOT, 1
ARRAYREMOVE IRAI_SUB,      CHK_SLOT, 1
ARRAYREMOVE IRAI_DEADLINE, CHK_SLOT, 1
CALL SET_IRAI_PROGRESS(CHARA, SCENE)



;----------------------------------------------------------------------------------------------------
;의뢰 발생 처리
;적당하게 10 캐릭터 보고 고쳐 의뢰를 만들어
;----------------------------------------------------------------------------------------------------
@IRAI_HAPPEN
#DIM CHARA_ID, 10
[IF_DEBUG]
	;#;PRINTFORMW 의뢰 생성 처리(전캐릭터 실행, 로그를 디버그 윈도우에 표시)
	;#;DEBUGCLEAR
	FOR LOCAL:0, 1, 캐릭터수상한
		SIF !CAN_MEET(LOCAL:0)
			CONTINUE
		SIF CFLAG:(LOCAL:0):의뢰상황
			CONTINUE
		CALL SET_NEW_IRAI(LOCAL:0)
	NEXT
	;#;PRINTFORML 의뢰 생성 완료, 디버그문을 삭제합니다
	;#;FORCEWAIT
	;#;DEBUGCLEAR
	RETURN
[ENDIF]
CALL ARRAY_RAND(CHARA_ID, 10, 1, 캐릭터수상한)
FOR LOCAL:0, 0, 10
	SIF !CAN_MEET(CHARA_ID:(LOCAL:0))
		CONTINUE
	SIF CFLAG:(CHARA_ID:(LOCAL:0)):의뢰상황
		CONTINUE
	CALL SET_NEW_IRAI(CHARA_ID:(LOCAL:0))
NEXT



;----------------------------------------------------------------------------------------------------
;각 캐릭터의 의뢰 발생 처리
;의뢰 없음/일반의뢰/고유 의뢰
;----------------------------------------------------------------------------------------------------
@SET_NEW_IRAI(CHARA)
#DIM CHARA
SELECTCASE IRAI_HIT(CHARA, "HAPPEN")
CASE IS <= 0
	RETURN
CASE 1
	LOCAL = IRAI_HIT(CHARA, "COMMON")
	SIF !LOCAL
		RETURN
CASE 2
	LOCAL = IRAI_HIT(CHARA, "UNIQUE")
	SIF LOCAL <= 100
		RETURN
ENDSELECT
CALL SET_IRAI_PROGRESS(CHARA, "의뢰 발생 직후")
CFLAG:CHARA:의뢰내용 = MAKE_IRAI_ID(CHARA, LOCAL)



;----------------------------------------------------------------------------------------------------
;의뢰 추첨 처리는 정리해 여기에
;----------------------------------------------------------------------------------------------------
@IRAI_HIT(CHARA, TYPE)
#FUNCTION
#DIM  CHARA
#DIMS TYPE
#DIM CONST LAST_COMMON = 99 ;일반의뢰ID의 말단
#DIM       LAST_UNIQUE      ;그 캐릭터에게 실장되고 있는 고유의뢰ID의 말단
#DIM       HIT_LIST, 100    ;추첨 확률 배열
#DIM       ID_LIST,  100    ;추첨을 대상 의뢰 ID의 일람
#DIM       CNT_LIST         ;추첨 대상이 되는 의뢰의 수
DEBUGPRINTFORML ＞IRAI_HIT{CHARA}　%TYPE%
SELECTCASE TYPE
CASE "HAPPEN"
;의뢰 발생 추첨
	HIT_LIST:0 = GET_INT(0, "캐릭터데이터", CHARA, "의뢰 없음 발생율")
	HIT_LIST:1 = GET_INT(0, "캐릭터데이터", CHARA, "일반의뢰발생율")
	HIT_LIST:2 = GET_INT(0, "캐릭터데이터", CHARA, "고유의뢰발생율")
	SIF !SUMARRAY(HIT_LIST, 0, 3)
		HIT_LIST = 7, 3, 0
	DEBUGPRINTFORML ＞IRAI_HIT{CHARA}　{HIT_LIST:0}/{HIT_LIST:1}/{HIT_LIST:2}
	RETURNF ARRAY_HIT(HIT_LIST, 3)
CASE "COMMON"
;일반의뢰의 추첨
	VARSET HIT_LIST
	VARSET ID_LIST
	VARSET CNT_LIST
	FOR LOCAL:0, 1, LAST_COMMON + 1
		SIF !INT_DATA_IRAI(MAKE_IRAI_ID(CHARA, LOCAL:0), "의뢰 발생 조건", CHARA)
			CONTINUE
		ID_LIST:CNT_LIST  = LOCAL:0
		HIT_LIST:CNT_LIST = INT_DATA_IRAI(MAKE_IRAI_ID(CHARA, LOCAL:0), "의뢰 발생율", CHARA)
		CNT_LIST ++
	NEXT
	SIF !CNT_LIST
		RETURNF 0
	RETURNF ID_LIST:(ARRAY_HIT(HIT_LIST, CNT_LIST))
CASE "UNIQUE"
;고유 의뢰의 추첨
	VARSET HIT_LIST
	VARSET ID_LIST
	VARSET CNT_LIST
	LAST_UNIQUE = GET_INT(0, "캐릭터데이터", CHARA, "고유의뢰최종 요소")
	FOR LOCAL:0, 101, LAST_UNIQUE + 1
		SIF !INT_DATA_IRAI(MAKE_IRAI_ID(CHARA, LOCAL:0), "의뢰 발생 조건", CHARA)
			CONTINUE
		ID_LIST:CNT_LIST  = LOCAL:0
		HIT_LIST:CNT_LIST = INT_DATA_IRAI(MAKE_IRAI_ID(CHARA, LOCAL:0), "의뢰 발생율", CHARA)
		CNT_LIST ++
	NEXT
	SIF !CNT_LIST
		RETURNF 0
	RETURNF ID_LIST:(ARRAY_HIT(HIT_LIST, CNT_LIST))
ENDSELECT



;----------------------------------------------------------------------------------------------------
;의뢰 소멸 처리
;전캐릭터가 대상이 되어 대규모화했다
;----------------------------------------------------------------------------------------------------
@IRAI_VANISH
#DIM CHK_SLOT
#DIM CHARA
;#;PRINTFORMW 의뢰 시간 경과 처리(전캐릭터 실행, 로그를 디버그 윈도우에 표시)
;#;DEBUGCLEAR
;전캐릭터 대상, 수탁전의/이미 보고가 끝난 의뢰의 처리
FOR CHARA, 1, CHARANUM
	SELECTCASE IRAI_CHARA_TO_PROGRESS(CHARA)
	;이것들은 아직 의뢰슬롯에 들어가 있지 않다
	CASE "의뢰 발생 직후"
		CALL SET_IRAI_PROGRESS(CHARA, "의뢰미수탁")
		
	CASE "의뢰미수탁"
		SIF INT_DATA_IRAI(CFLAG:CHARA:의뢰내용, "의뢰 소멸율", CHARA)
			CALL SET_IRAI_PROGRESS(CHARA, "의뢰 소멸")
	[SKIPSTART]
	의뢰중의 케이스는 의뢰슬롯보는 편이 빠르다
	CASE "의뢰중"
	
	기본적으로 이 4 단계는 미 보고 상태, 의뢰주의 리액션 대기입니다( 보고 필수)
	CASE "의뢰 달성"
	CASE "기한 마감"
	CASE "보고 잊고"
	CASE "의뢰 실패"
	[SKIPEND]
	CASE "의뢰 종료(성공)", "의뢰 종료(기한 마감)", "의뢰 종료( 보고 잊고)", "의뢰 종료(실패)"
	;의뢰가 종료해, 의뢰인에게 보고를 행한 패턴(슬롯에 들어 있지 않다)
		CALL SET_IRAI_PROGRESS(CHARA, "의뢰 소멸")
	ENDSELECT
NEXT
;#;PRINTFORML 의뢰 시간 경과 처리 완료, 디버그문을 삭제합니다
;#;FORCEWAIT
;#;DEBUGCLEAR

SIF !COUNT_IRAI_SLOT()
	RETURN

;#;PRINTFORMW 의뢰기한처리(전캐릭터 실행, 로그를 디버그 윈도우에 표시)
;의뢰의 기한 판정
;슬롯은 비우지 않기로 했다(의뢰주에의 보고를 강제시킨다)
;의뢰 보고 불필요시에 슬롯을 비우는 처리가 필요하게 되었다
;슬롯 1으로부터 봐 간다고 의뢰의 리셋에 의해 ID엇갈림이 발생하므로 말미로부터 체크한다
FOR CHK_SLOT, COUNT_IRAI_SLOT(), 0, -1
	CHARA = IRAI_SLOT_TO_CLIENT(CHK_SLOT)
	;기한에 의한 의뢰의 실패 판정
	;기한이 남고 있다면 패스
	SIF IRAI_DEADLINE:CHK_SLOT && DAY <= IRAI_DEADLINE:CHK_SLOT
		CONTINUE
	SELECTCASE IRAI_CHARA_TO_PROGRESS(CHARA)
	CASE "의뢰중"
		CALL SET_IRAI_PROGRESS(CHARA, "기한 마감")
	CASE "의뢰 달성"
		IF STRLENS(STR_DATA_IRAI(IRAI_SLOT:CHK_SLOT, "의뢰 보고 불필요", CHARA))
			CALL IRAI_CARRYOUT(CHARA, IRAI_SLOT:CHK_SLOT, "의뢰 보고 불필요")
			CALL SHOW_IRAI_MESSAGE(CHARA, IRAI_SLOT:CHK_SLOT, "의뢰 보고 불필요")
			CALL CLEAR_IRAI_SLOT(CHARA, CHK_SLOT, "의뢰 소멸")
		ELSE
			CALL SET_IRAI_PROGRESS(CHARA, "보고 잊고")
		ENDIF
	ENDSELECT
NEXT
;#;PRINTFORML 의뢰기한처리 완료, 디버그문을 삭제합니다
;#;FORCEWAIT
;#;DEBUGCLEAR


;----------------------------------------------------------------------------------------------------
;의뢰내용의 실행 처리
;실행시의 SOURCE의 변화의 외, 보수나 실패에 의한 처리도 포함한다
;----------------------------------------------------------------------------------------------------
@IRAI_CARRYOUT(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
RESULT = 0
IF IS_COMMON_IRAI(IRAI_ID)
	TRYCCALLFORM IRAI_일반{IRAI_ID % 1000}(CHARA, IRAI_ID, SCENE)
	CATCH
		PRINTFORML *에러 발생*
		PRINTFORML 　CHARA:{CHARA}　SCENE:%SCENE%　IRAI_ID:{IRAI_ID}
		PRINTFORML 　지정의 함수 @IRAI_일반{IRAI_ID % 1000}은 존재하지 않습니다
		PRINTFORML 　에러 로그를 코피페 해 보고해 주세요
		PRINTFORML 
		PRINTFORMW 　입력 대기중
	ENDCATCH
ELSE
	TRYCCALLFORM IRAI_고유{IRAI_ID_TO_CLIENT(IRAI_ID)}(CHARA, IRAI_ID, SCENE)
	CATCH
		PRINTFORML *에러 발생*
		PRINTFORML 　CHARA:{CHARA}　SCENE:%SCENE%　IRAI_ID:{IRAI_ID}
		PRINTFORML 　지정의 함수 @IRAI_고유{IRAI_ID_TO_CLIENT(IRAI_ID)}는 존재하지 않습니다
		PRINTFORML 　에러 로그를 코피페 해 보고해 주세요
		PRINTFORML 
		PRINTFORMW 　입력 대기중
	ENDCATCH
ENDIF
RETURN RESULT
;RESULT와 의뢰실행 판정 결과의 대응은 IRAI_CONST_RESULT(TYPE)를 참조



;----------------------------------------------------------------------------------------------------
;수탁이 끝난 의뢰 정보 표시 함수
;----------------------------------------------------------------------------------------------------
@SHOW_ALL_IRAI_SLOT
#DIM CHK_SLOT
#DIM IRAI_NUM
IRAI_NUM = COUNT_IRAI_SLOT()
FOR CHK_SLOT, 1, IRAI_NUM + 1
	CALL SHOW_IRAI_DATA(IRAI_SLOT:CHK_SLOT, CHK_SLOT)
	PRINTFORML 
NEXT



;----------------------------------------------------------------------------------------------------
;의뢰 정보 표시 함수
;의뢰 ID단체에서도 사용할 수 있지만 수탁 끝난 경우는 간략화를 위해서(때문에) SLOT 번호를 건네준다
;풀네임 표기인 것은 원 자리와입니다(그 편이 딱딱한 문면 같게 보이므로)
;----------------------------------------------------------------------------------------------------
@SHOW_IRAI_DATA(IRAI_ID, CHK_SLOT)
#DIM IRAI_ID   ;대상의 의뢰 ID(의뢰인 정보 포함한다)
#DIM CHK_SLOT  ;판정 대상으로 하는 의뢰슬롯, 미수탁 상태에서는 0으로 읽어 주세요
#DIM CLIENT_ID ;의뢰인 ID
#DIM CHK_ID    ;의뢰 자체의 ID
#DIM DEAD_LINE ;기한 날짜
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
CHK_ID    = IRAI_ID % 1000
DEAD_LINE = INT_DATA_IRAI(IRAI_ID, "의뢰기한", CLIENT_ID)
SIF CHK_SLOT
	PRINTFORML 의뢰 %TOFULL(TOSTR(CHK_SLOT))%
PRINTFORML 　＜%STR_DATA_IRAI(IRAI_ID, "의뢰명", CLIENT_ID)%＞
PRINTFORML 　　의뢰자：%NAME:CLIENT_ID%
PRINTFORML 　의뢰내용：%STR_DATA_IRAI(IRAI_ID, "의뢰내용 표시", CLIENT_ID)%
PRINTFORML 　　　보수：%STR_DATA_IRAI(IRAI_ID, "의뢰 보수 표시", CLIENT_ID)%
SIF STRLENS(STR_DATA_IRAI(IRAI_ID, "의뢰 보고 불필요", CLIENT_ID))
	PRINTFORML 　　　보고：%STR_DATA_IRAI(IRAI_ID, "의뢰 보고 불필요", CLIENT_ID)%
SIF STRLENS(STR_DATA_IRAI(IRAI_ID, "의뢰 비고 표시", CLIENT_ID))
	PRINTFORML 　　　비고：%STR_DATA_IRAI(IRAI_ID, "의뢰 비고 표시", CLIENT_ID)%
;#;PRINTFORML 　　의뢰 ID：{IRAI_ID}　CLIENT_ID{CLIENT_ID}

;미수탁의 경우는 기한만 표시해 끝
IF !CHK_SLOT
	IF !DEAD_LINE
		PRINTFORML 　　　기한：오늘중
	ELSE
		PRINTFORML 　　　기한：{DEAD_LINE}일 이내
	ENDIF
	RETURN
ENDIF

;여기에서 수탁 끝나, 업종별의 표시
SELECTCASE IRAI_SLOT_TO_TASKNAME(CHK_SLOT)
CASE "조달"
	PRINTFORML 　　요구품：%ITEMNAME:(IRAI_SLOT_TO_ITEM(CHK_SLOT))%×{IRAI_SUB:CHK_SLOT}
CASE "전령(인물)"
	PRINTFORML 　목표 인물：%NAME:(IRAI_SLOT_TO_PERSON(CHK_SLOT))%
CASE "전령(장소)"
	PRINTFORML 　　목적지：%GET_PLACENAME(IRAI_SLOT_TO_PLACE(CHK_SLOT))%
;나즈린에 들어도 잊는 당신( 나)에게
CASE "수색(인물)"
	SIF IRAI_SUB:CHK_SLOT
		PRINTFORML 　참고인물：%CALLNAME:(IRAI_SUB:CHK_SLOT)%
CASE "수색(장소)"
	IF IRAI_SUB:CHK_SLOT > 5000
		PRINTFORML 　　추정지：%STR:(5000 + GET_MAPID(IRAI_SUB:CHK_SLOT-5000))%
	ELSEIF IRAI_SUB:CHK_SLOT
		PRINTFORML 　　추정지：%STR:(5000 + GET_MAPID(IRAI_SUB:CHK_SLOT))%의 %GET_PLACENAME(IRAI_SUB:CHK_SLOT)%
	ENDIF
ENDSELECT
CALL IRAI_CARRYOUT(CLIENT_ID, IRAI_ID, "의뢰 조건 표시")

;확정하고 있는 경우는 그 취지를 표시해 끝
IF GROUPMATCH(IRAI_CHARA_TO_PROGRESS(CLIENT_ID), "기한 마감", "보고 잊고", "의뢰 실패")
	PRINTFORML 　달성 상황：실패
	RETURN
ELSEIF GROUPMATCH(IRAI_CHARA_TO_PROGRESS(CLIENT_ID), "의뢰 달성")
	PRINTFORML 　달성 상황：달성이 끝난 상태(미 보고)
	RETURN
ELSEIF !DEAD_LINE
	PRINTFORML 　　　기한：오늘중
ELSEIF DAY > IRAI_DEADLINE:CHK_SLOT
	PRINTFORML 　　　기한：나머지{IRAI_DEADLINE:CHK_SLOT - DAY}일
ELSEIF DAY == IRAI_DEADLINE:CHK_SLOT
	PRINTFORML 　　　기한：오늘중
ENDIF



;----------------------------------------------------------------------------------------------------
;의뢰 관련의 말호출 함수
;----------------------------------------------------------------------------------------------------
@SHOW_IRAI_MESSAGE(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
SIF FLAG:구상텍스트설정 <= 0
	RETURN
SIF FLAG:대사색깔
	TRYCALLFORM M_KOJO_COLOR_K{NO:CHARA}
IF SCENE == "의뢰 보고 불필요"
	TRYCALLFORM M_KOJO_IRAI_K{CHARA}("NO_REPORT", SCENE, IRAI_ID)
ELSEIF CHARA == IRAI_ID_TO_CLIENT(IRAI_ID)
;대상이 의뢰자이다
	TRYCALLFORM M_KOJO_IRAI_K{CHARA}("CLIENT", SCENE, IRAI_ID)
ELSE
;이 경우는 대개 SCENE는"의뢰실행시"가 되지만 일단 건네준다
	TRYCALLFORM M_KOJO_IRAI_K{CHARA}("TARGET", SCENE, IRAI_ID)
ENDIF
RESETCOLOR


;----------------------------------------------------------------------------------------------------
;의뢰 리셋 함수
;의뢰 관련 변수의 전초기화
;----------------------------------------------------------------------------------------------------
@IRAI_RESET
PRINTFORML 모든 의뢰를 리셋합니다.
PRINTFORML 괜찮겠습니까?
CALL ASK_YN
IF !RESULT
	CVARSET CFLAG, 403
	CVARSET CFLAG, 404
	VARSET IRAI_SLOT
	VARSET IRAI_DATA
	VARSET IRAI_SUB
	VARSET IRAI_DEADLINE
	VARSET IRAI_CLIENT
	PRINTFORMW 리셋했습니다.
ENDIF



;----------------------------------------------------------------------------------------------------
;의뢰기한통지 함수
;매일 아침의 통지
;----------------------------------------------------------------------------------------------------
@MEMO_IRAI_DEADLINE
#DIM CLIENT
;수탁이 끝난 의뢰의 기한의 처리
FOR LOCAL, 1, 6
	SIF !IRAI_SLOT:LOCAL
		CONTINUE
	CLIENT = IRAI_SLOT_TO_CLIENT(LOCAL)
	SIF GROUPMATCH(IRAI_CHARA_TO_PROGRESS(CLIENT), "의뢰중", "의뢰 달성", "의뢰 실패")
		PRINTFORML %CALLNAME:CLIENT%의 의뢰의 기한은 나머지{IRAI_DEADLINE:LOCAL - DAY}일이다
NEXT



;----------------------------------------------------------------------------------------------------
;「방을방문한다」에서의 의뢰 관련의 액션
;----------------------------------------------------------------------------------------------------
@IRAI_VISIT_HOME(CHARA)
#DIM PREV_TARGET     ;TARGET 퇴피
#DIM CHARA           ;대상 꺄등
#DIM IRAI_NUM        ;수탁 의뢰수
#DIM CHK_SLOT        ;판정 대상 슬롯
#DIM DYNAMIC CNT_DO  ;실행/보고한 의뢰수=반환값
PREV_TARGET = TARGET
TARGET = CHARA
IRAI_NUM = COUNT_IRAI_SLOT()
FOR CHK_SLOT, 1, IRAI_NUM + 1
	SIF !CAN_IRAI_VISIT_HOME_PERSON(CHARA, CHK_SLOT)
		CONTINUE
	PRINTFORML ＜%STR_DATA_IRAI(IRAI_SLOT:CHK_SLOT, "의뢰명", CHARA)%＞
	PRINTFORMW 　→[%IRAI_SLOT_TO_ACTNAME(CHK_SLOT)%]
	CALL IRAI_APPLY(CHK_SLOT)
	SIF RESULT > 0
		CNT_DO ++
NEXT

IRAI_NUM = COUNT_IRAI_SLOT()
FOR CHK_SLOT, IRAI_NUM, 0, -1
	SIF !CAN_IRAI_VISIT_HOME_CLIENT(CHARA, CHK_SLOT)
		CONTINUE
	PRINTFORML ＜%STR_DATA_IRAI(IRAI_SLOT:CHK_SLOT, "의뢰명", CHARA)%＞
	PRINTFORMW 　→의뢰 보고
	CALL IRAI_REPORT(CHARA)
	SIF RESULT > 0
		CNT_DO ++
NEXT
TARGET = PREV_TARGET
RETURN CNT_DO
