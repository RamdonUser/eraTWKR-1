;-------------------------------------------------
;술집
;외출 커맨드
;-------------------------------------------------
@COM619
#DIM 탁주판매개수
#DIM 청주판매개수
#DIM 오니고로시판매개수
#DIM 재고증가수
;RAND를 걸치는 것은 일일일회
IF FLAG:마지막에술집에방문한날 != DAY
	;연회 당일은 술이 품귀상태로
	IF FLAG:개시일 == DAY
		탁주판매개수 = RAND:(4 + FLAG:술집매각율플래그 * 4)
		청주판매개수     = RAND:(3 + FLAG:술집매각율플래그 * 3)
		오니고로시판매개수   = RAND:(2 + FLAG:술집매각율플래그 * 2)
	ELSE
		탁주판매개수 = RAND:(6 + FLAG:술집매각율플래그 * 8)
		청주판매개수     = RAND:(5 + FLAG:술집매각율플래그 * 6)
		오니고로시판매개수   = RAND:(4 + FLAG:술집매각율플래그 * 4)
	ENDIF
	;누적 구입액에 응해도 재고가 증가
	IF FLAG:술집에서누적구입액
		재고증가수 = MAX(FLAG:술집에서누적구입액 / 100000 - 2, 0) + 1
		탁주판매개수 += RAND:재고증가수
		청주판매개수     += RAND:재고증가수
		오니고로시판매개수   += RAND:재고증가수
	ENDIF
ENDIF

IF FLAG:마지막에술집에방문한날 == 0
	PRINTL 「첫 손님이구나? 서비스할게」
ELSEIF DAY - FLAG:마지막에술집에방문한날 > 7
	PRINTL 「오랜만이네요―, 천천히 둘러봐요」
ELSEIF FLAG:마지막에술집에방문한날 == DAY
	PRINTL 「어서 오세요, 응, 어머머? 웬 일이야?」
ELSEIF FLAG:술집에서누적구입액 > 1000000 && FLAG:개시일 != DAY
	PRINTFORML 「어서 오세요, %마스터를% 위해서 널럴하게 들여놨지」
ELSEIF FLAG:개시일 == DAY
	PRINTL 「때가 안 좋았네. 아까 온 손님이 거의 다 사갔어」
ELSEIF 탁주판매개수 + 청주판매개수 + 오니고로시판매개수 > 30
	PRINTL 「어서 오세요, 좋은 술이 많이 있어―」
ELSE
	PRINTL 「어서 오십시오―」
ENDIF
SIF FLAG:술집매입강화
	PRINTFORML 「현재 매입 강화 캠페인 중이야. 물건 조달해주면 톡톡히 쳐줄게」
$INPUT_LOOP2
PRINTL [0] - 술을 산다
PRINTL [1] - 술을 판다
PRINTL [100] - 돌아간다
$INPUT_LOOP
INPUT

IF RESULT == 0
	CALL 술구입(탁주판매개수, 청주판매개수, 오니고로시판매개수)
	GOTO INPUT_LOOP2
ELSEIF RESULT == 1
	CALL 술도매(탁주판매개수, 청주판매개수, 오니고로시판매개수)
	GOTO INPUT_LOOP2
ELSEIF RESULT != 100
	GOTO INPUT_LOOP
ENDIF

FLAG:마지막에술집에방문한날 = DAY
IF FLAG:술집누적전매이익 > 3000000 && FLAG:술집매각율플래그 == 2
	FLAG:술집매각율플래그 = 3
	PRINTL 
	PRINTL 가게에서 나가려 했는데 불러 세웠다
	PRINTL 「두둑하게 술을 조달해줘서 정말 살겠어∼, 앞으로는 매입 금액 더 쳐줄게」
ELSEIF FLAG:술집누적전매이익 > 1000000 && FLAG:술집매각율플래그 == 1
	FLAG:술집매각율플래그 = 2
	PRINTL 
	PRINTL 가게에서 나가려 했는데 불러 세웠다
	PRINTL 「두둑하게 술을 조달해줘서 살겠어―, 매입 금액 더 쳐줄 테니 더 조달해주지 않을래?」
ELSEIF FLAG:술집누적전매이익 > 100000 && FLAG:술집매각율플래그 == 0
	FLAG:술집매각율플래그 = 1
	PRINTL 
	PRINTL 가게에서 나가려 했는데 불러 세웠다
	PRINTL 「늘 술을 조달해줘서 고마워. 사례로 다음 번부터 매입 금액 더 쳐줄게―」
ELSE
	PRINTL 「또 오세요―」
ENDIF
TIME += 10
RETURN -1

@술구입(탁주판매개수, 청주판매개수, 오니고로시판매개수)
#DIM REF 탁주판매개수
#DIM REF 청주판매개수
#DIM REF 오니고로시판매개수
#DIM 판매개수
#DIM 소지가능개수
#DIM 가격인하율
#DIM 선택한상품
#DIM 구입개수
#DIM 분명하게샀다

;가격인하율을 20%로 설정(정가의 80%로 판매)
가격인하율 = 20

;각각의 술의 최대 소지 가능수를 80으로 설정(80개 이상 가지고 있다고(면) 그 술은 살 수 없습니다)
소지가능개수 = 80

PRINTL 
IF FLAG:마지막에술집에방문한날 == 0
	PRINTL 「처음 왔으니 서비스야―」
	;가격인하율을 40%로 설정(정가의 60%로 판매)
	가격인하율 = 40
ELSE
	PRINTL 「어느 거나 사서 손해 없어―」
ENDIF
$INPUT_LOOP2
PRINTFORML 소지금 :\\%TOSTR(MONEY,"#,###")%
FOR LOCAL, 51,54
	SELECTCASE LOCAL
		CASE 51
			판매개수 = 탁주판매개수
		CASE 52
			판매개수 = 청주판매개수
		CASE 53
			판매개수 = 오니고로시판매개수
	ENDSELECT
	LOCALS = [{LOCAL}]%ITEMNAME:(LOCAL), 8, LEFT%	(\\%TOSTR(ITEMPRICE:(LOCAL) * (100 - 가격인하율) / 100), 5, RIGHT%) 
	IF ITEMPRICE:LOCAL
		PRINTFORM %LOCALS% 
		IF 판매개수 == 0
			SETCOLOR C_RED
			PRINT 	(다 팔림) 
			RESETCOLOR
		ELSE
			PRINTFORM 	(재고:{판매개수}) 
		ENDIF
		IF ITEM:LOCAL >= 소지가능개수
			SETCOLOR C_YELLOW
			PRINTFORM \t소지:{ITEM:LOCAL, 2, RIGHT} 
			FONTSTYLE 1
			PRINTL 구입 불가
			RESETCOLOR
			FONTREGULAR
		ELSE
			PRINTFORML \t소지:{ITEM:LOCAL, 2, RIGHT}
		ENDIF
	ENDIF
NEXT
PRINT [100] - 돌아간다
$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN -1
ELSEIF RESULT >= 51 && RESULT <= 53
	
	;나중에 다시 한번 INPUT를 사용하기 위해 지금의 값을 퇴피
	선택한상품 = RESULT
	
	;선택한 번호에 따라판매개수를 격납
	SELECTCASE 선택한상품
		CASE 51
			판매개수 = 탁주판매개수
		CASE 52
			판매개수 = 청주판매개수
		CASE 53
			판매개수 = 오니고로시판매개수
	ENDSELECT
	
	;품절의 경우
	IF 판매개수 == 0
		PRINTL 
		PRINTL 「미안해요. 다 팔렸어요」
		
	;최대 소지 가능수이상의 술을 소지하고 있는 경우
	ELSEIF ITEM:선택한상품 >= 소지가능개수
		PRINTL 「손님, 그만큼 있으면 충분하잖아요? 오히려 우리한테 물건을 대주라―」
	
	;그 이외의경우 개수를 묻는다
	ELSE
		구입개수 = 0
		$BUY_LOOP
		PRINTFORML %ITEMNAME:선택한상품%를 몇 개 살까…
		PRINTFORML 사는 수：{구입개수}
		PRINTFORML [ -10]　[  -1]　[  +1]　[ +10]
		PRINTBUTTON "[전부] ", 999
		PRINTBUTTON " [리셋]", -999
		PRINTL 
		PRINTBUTTON "[산다]", 0
		INPUT
		IF RESULT
			IF RESULT == 999
				구입개수 =판매개수
			ELSEIF RESULT == -999
				구입개수 = 0
			ELSE
				구입개수 = LIMIT(구입개수 + RESULT, 0,판매개수)
			ENDIF
			CLEARLINE 6
			GOTO BUY_LOOP
		ENDIF
		;0이하의 값을 입력했을 경우
		IF 구입개수 <= 0
			PRINTL 
			PRINTW 역시 사는 건 그만두자
			GOTO INPUT_LOOP2
		;재고의 수보다 많이 구입하려고 했을 경우
		ELSEIF 판매개수 < 구입개수
			PRINTW 재고가 그렇게 많진 않은 것 같다
			GOTO INPUT_LOOP2
		;최대 소지 가능수보다 많이 구입하려고 했을 경우
		ELSEIF 소지가능개수 < ITEM:선택한상품 + 구입개수
			PRINTW 그렇게나 가져봤자 처치곤란하다
			GOTO INPUT_LOOP2
		ENDIF
		CALL BUY_ABLE(선택한상품, 0, 구입개수, 가격인하율)
		분명하게샀다 = RESULT
		IF FLAG:술집에서누적구입액 > 100000 && !RAND:9 && 분명하게샀다
			PRINTL 
			PRINTL 「항상 사줘서 고마워, 이건 덤이야」
			PRINTFORMW ―덤으로 %ITEMNAME:510%를 받았다-
			ITEM:510 ++
		ELSE
			PRINTL 
			PRINTW 「매번 감사―」
		ENDIF
		IF 분명하게샀다
			FLAG:술집에서누적구입액 += ITEMPRICE:선택한상품 * (100 - 가격인하율) / 100 * 구입개수
			;판매개수를 감소시키는 처리
			SELECTCASE 선택한상품
				CASE 51
					탁주판매개수 -= 구입개수
				CASE 52
					청주판매개수     -= 구입개수
				CASE 53
					오니고로시판매개수   -= 구입개수
			ENDSELECT
		ENDIF
	ENDIF
	GOTO INPUT_LOOP2
ELSE
	GOTO INPUT_LOOP2
ENDIF
RETURN

@술도매(탁주판매개수, 청주판매개수, 오니고로시판매개수)
#DIM REF 탁주판매개수
#DIM REF 청주판매개수
#DIM REF 오니고로시판매개수
#DIM 가격인하율
#DIM 소지수
#DIM 선택한상품
#DIM 매각개수
#DIM 매각액

SELECTCASE FLAG:술집매각율플래그
	CASE 0
		;가격인하율을 90%로 설정(정가의 10%로 구입해 준다)
		가격인하율 = 90
	CASE 1
		;가격인하율을 85%로 설정(정가의 15%로 구입해 준다)
		가격인하율 = 85
	CASE 2
		;가격인하율을 80%로 설정(정가의 20%로 구입해 준다)
		가격인하율 = 80
	CASE 3
		;가격인하율을 70%로 설정(정가의 30%로 구입해 준다)
		가격인하율 = 70
ENDSELECT
SIF FLAG:술집매입강화
	가격인하율 -= 20
PRINTL 
PRINT 「
IF FLAG:술집매입강화
	PRINT 매입 강화 캠페인중이니까 특별히
ELSEIF FLAG:술집매각율플래그
	PRINT 고객이니까 특별히
ENDIF
PRINTFORML 정가의 {100 - 가격인하율}\%로 살게」
PRINTL 무엇을 팔까…
$INPUT_LOOP2
PRINTFORML 소지금 :\\%TOSTR(MONEY,"#,###")%
FOR LOCAL, 51,54
	LOCALS = [{LOCAL}]%ITEMNAME:(LOCAL)%
	IF ITEMPRICE:LOCAL
		PRINTFORML %LOCALS%	(\\{ITEMPRICE:(LOCAL) * (100 - 가격인하율) / 100})  	(소지:{ITEM:LOCAL})
	ENDIF
NEXT
PRINT [100] - 돌아간다
$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN -1
ELSEIF RESULT >= 51 && RESULT <= 53
	;선택한 번호에 따라 소지수를 격납
	소지수 = ITEM:RESULT
	
	;소지 0의 경우
	IF 소지수 == 0
		PRINTL 
		PRINTL 물건이 없기에 팔고 싶어도 팔 수 없다
	;소지 0으로 없는 경우 개수를 묻는다
	ELSE
		;다시 한번 INPUT를 사용하기 때문에 지금의 값을 퇴피
		선택한상품 = RESULT
		매각개수 = 0
		$SELL_LOOP
		PRINTFORML %ITEMNAME:선택한상품%를 몇 개 팔까…
		PRINTFORML 파는 수：{매각개수}
		PRINTFORML [-100]　[ -10]　[  -1]　[  +1]　[ +10]　[+100]
		PRINTBUTTON "[전부] ", 999
		PRINTBUTTON " [리셋]", -999
		PRINTL 
		PRINTBUTTON "[판다]", 0
		INPUT
		IF RESULT
			IF RESULT == 999
				매각개수 = 소지수
			ELSEIF RESULT == -999
				매각개수 = 0
			ELSE
				매각개수 = LIMIT(매각개수 + RESULT, 0, 소지수)
			ENDIF
			CLEARLINE 6
			GOTO SELL_LOOP
		ENDIF
		IF 매각개수 <= 0
			PRINTL 
			PRINTL 역시 파는 건 그만두자
			GOTO INPUT_LOOP2
		ENDIF
		IF 매각개수 > 소지수
			PRINTL 
			PRINTL 그렇게나 많이 가지고 있지 않다
			GOTO INPUT_LOOP2
		ENDIF
		
		;1개당의 매각값을 산출
		매각액 = ITEMPRICE:선택한상품 * (100 - 가격인하율) / 100
		ITEM:선택한상품 -= 매각개수
		MONEY += 매각액 * 매각개수
		;가게는 정가의 80%로 판매
		FLAG:술집누적전매이익 += ITEMPRICE:선택한상품 * (가격인하율 - 20) / 100 * 매각개수
		PRINTFORMW %ITEMNAME:선택한상품%를 ￥{매각액*매각개수}로 {매각개수}개 매각했습니다
		;판매개수를 증가시키는 처리
		SELECTCASE 선택한상품
		CASE 51
			탁주판매개수 += 매각개수
		CASE 52
			청주판매개수     += 매각개수
		CASE 53
			오니고로시판매개수   += 매각개수
		ENDSELECT
	ENDIF
	GOTO INPUT_LOOP2
ELSE
	GOTO INPUT_LOOP
ENDIF
;-------------------------------------------------
;실행 가능 판정
;-------------------------------------------------
;술집
@COM_ABLE619
;일괄관리
SIF GLOBAL_COMABLE(619)
	RETURN RESULT
IF !AT_HOME(MASTER)
	SIF CFLAG:MASTER:현재위치 != 술집소로
		RETURN 0
ELSE
	SIF CFLAG:MASTER:현재위치 != 211
		RETURN 0
ENDIF
;수면중
SIF CFLAG:수면
	RETURN 0
;시간정지중
SIF FLAG:70
	RETURN 0
RETURN 1


