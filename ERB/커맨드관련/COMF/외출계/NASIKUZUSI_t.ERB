@ONABARE(ARG)
#DIM 이루어무너뜨려필요치
#DIM 부디그대로호감도상승치
#DIM TARGET보존

CFLAG:ARG:마지막에만난날 = DAY
;EVENT 말을 직접 부르므로 TARGET를 포착
TARGET보존 = TARGET
TARGET = ARG

IF !TALENT:ARG:처녀 && ABL:ARG:A감각 >= ABL:ARG:V감각
	LOCAL:2 = 1
ELSE
	LOCAL:2 = 0
ENDIF


PRINTFORMW \@CFLAG:MASTER:311 == CFLAG:ARG:311?  방의 밖으로부터 모습을 엿본# 열쇠가 열려 있었으므로 마음대로 올랐다\@
PRINTFORMW 안쪽으로부터 흐려진 목소리가 들린다……

;말체크
TRYCCALLFORM M_KOJO_EVENT_K{ARG}_ONABARE_1(LOCAL:2)
	
CATCH
	PRINTFORMW 눈치채지지 않게 방안을 들여다 보면(자), %CALLNAME:ARG%가 자위에 빠지고 있었다
	PRINTFORM 옷을은만 아랫배를 노출 %CALLNAME:ARG%는,
	IF TALENT:ARG:연모
		PRINTFORM %CALLNAME:MASTER%의 이름을 불러
	;심약계, 진면목계
	ELSEIF CFLAG:ARG:성격경향 == 1 || CFLAG:ARG:성격경향 == 4
		PRINTFORM 소리를 눌러 참아
	;강경세계
	ELSEIF CFLAG:ARG:성격경향 == 2 || CFLAG:ARG:성격경향 == 3 || CFLAG:ARG:성격경향 == 5
		PRINTFORM 단 소리를 높이고
	ENDIF
	PRINTFORMW 면서, 천천히 손가락을 움직이고 있다

	;큰 가슴
	IF TALENT:ARG:가슴사이즈 > 0
		PRINTFORM 출렁하고 흔들렸다 풍만한 유방
	;있고
	ELSEIF TALENT:ARG:가슴사이즈 < 0
		PRINTFORM 부들부들 떨리는 얇은 앞가슴
	;보통
	ELSE
		PRINTFORM 파르르 흔들리는 젖가슴
	ENDIF
	PRINTFORMW 를 어루만지고 돌려, 다리를 벌려 은밀한 곳 쪽으로 손가락을 가져 가는 %CALLNAME:ARG%
	PRINTFORMW 그대로, 스윽과\@ !LOCAL:2 ?  은밀한 곳 # 아누스 \@에 중지를 찔러넣는 모습에 군침을 삼킨다
	PRINTFORMW 동시, 한편 응과 문에 이마가 아얏이라고 끝냈다
	PRINTFORML 내방자를 알아차린%CALLNAME:ARG%는 경직되어, 몸을 숨기는 것조차 잊어 %CALLNAME:MASTER% 쪽을 응시하고 있다……
ENDCATCH

CALL KOJO_MESSAGE_SEND("EVENT", 26, ARG, 0,0)
PRINTFORMW 
$LOOP

PRINTFORML 어떻게 하지?
CALL ASK_M("실례했습니다", 1,"보아 줄 테니까 부디 그대로", 1,"한사람부터 두 명으로 하는 (분)편이 기분이 좋아", BASE:MASTER:정력 > MAXBASE:MASTER:정력 / 5)
IF RESULT == 0
	IF CFLAG:MASTER:311 == CFLAG:ARG:311
		PRINTFORML %마스터는% 서둘러 침상에 도착했다
	ELSE
		PRINTFORML %마스터는% 눈앞의 광경을 망막에 새긴 다음, 동요하는 %CALLNAME:ARG%가 제 정신을 차리는 앞에 한마디 사과해 떠났다
	ENDIF
	SIF !CFLAG:ARG:약점잡힘
		PRINTFORML %CALLNAME:ARG%의【약점】을 잡았다
	CALL KOJO_MESSAGE_SEND("EVENT", 26, ARG, 1,0)
	CFLAG:ARG:딸딸이들킴 = 1
	CFLAG:ARG:약점잡힘 = 1
	TIME += 5
ELSEIF RESULT == 1
	이루어무너뜨려필요치 = 껴안고필요치(ARG) - 50 + TALENT:ARG:수치심 * 50 - ABL:ARG:노출벽 * 30 - CFLAG:ARG:욕구불만도 / 20
	SIF  TALENT:ARG:자존심 > 0
		이루어무너뜨려필요치 += 100
	SIF CFLAG:ARG:기정사실 & 합의_우후후
		이루어무너뜨려필요치 -= 50
	SIF TALENT:ARG:담력 < 0
		이루어무너뜨려필요치 -= 200
	CALL SHOW_LOVEPOWER(CFLAG:ARG:합의판정, 50)
	CALL SHOW_REASONWALL(이루어무너뜨려필요치 , 50)
	;실패
	IF CFLAG:ARG:합의판정 <= 이루어무너뜨려필요치
		GOTO GOOUT
	;성공
	ELSE
		CALL KOJO_MESSAGE_SEND("EVENT", 26, ARG, 2,0)
		;말체크
		TRYCCALLFORM M_KOJO_EVENT_K{NO:ARG}_ONABARE_2(LOCAL:2)
		CATCH
			PRINTFORM 잠깐
			IF TALENT:ARG:연모
				PRINTFORM 기쁨과 당황스러움을 띄우고
			;심약계, 진면목계
			ELSEIF CFLAG:ARG:성격경향 == 1 || CFLAG:ARG:성격경향 == 4
				PRINTFORM 머뭇머뭇 쑥스러운 듯하게 해
			;강경세계, 솔직함계, 그 외
			ELSEIF CFLAG:ARG:성격경향 == 2 || CFLAG:ARG:성격경향 == 3 || CFLAG:ARG:성격경향 == 5
				PRINTFORM 기가 막힌듯한 곤란한 것 같은 얼굴을 해
			ENDIF
			PRINTFORML 라고 있던%CALLNAME:ARG%가, 작게 한숨을 쉬었다
			IF TALENT:ARG:연인
				PRINTFORML 옷통에 옷을 벗어 던져%CALLNAME:MASTER%에 정면으로 맞서 크게 다리를 벌렸다
			;심약계, 진면목계
			ELSEIF CFLAG:ARG:성격경향 == 1 || CFLAG:ARG:성격경향 == 4
				PRINTFORML 부끄러운 듯이 조금 옷을 흐트러졌다만으로, 다리는 닫은 채 그대로다
			;강경세계, 솔직함계, 그 외
			ELSEIF CFLAG:ARG:성격경향 == 2 || CFLAG:ARG:성격경향 == 3 || CFLAG:ARG:성격경향 == 5
				PRINTFORML 옷을 벗으면서, 그런데도 중요한 부분은 손으로 숨기고 있다
			ENDIF
			PRINTFORML 그런, %CALLNAME:ARG%에, 계속을 재촉하는 %CALLNAME:MASTER%
			PRINTFORMW 작게 한숨을 쉰%CALLNAME:ARG%가 천천히, 손을 움직이기 시작했다

			PRINTFORML 입과 소리를 내며\@ !LOCAL:2 ?  질구 # 항문 \@으로 메워지는 중지
			;큰 가슴
			IF TALENT:가슴사이즈 > 0
				PRINTFORML 에  손가락이 메워져 모양을 바꾸는 유방
			;있고
			ELSEIF TALENT:가슴사이즈 < 0
				PRINTFORML 빙글빙글 손가락의 끝이 비비어 세우는 유두
			;보통
			ELSE
				PRINTFORML 포근포근하고 손으로 흔들어지고 있는 젖가슴
			ENDIF
			PRINTFORMW 그 선정적인 광경을 한층 더 추잡하게 하는 단 소리가, 영향을 주기 시작했다
			IF TALENT:ARG:연인
				PRINTFORML 염치없고, %CALLNAME:MASTER%의 이름을 부르면서, 격렬하게 손가락을 움직이기 시작했다
				PRINTFORML 질퍽하게, 위와 아래의 입으로부터 군침을 늘어뜨려, 황홀해 하는 표정을 향하여 오는 %CALLNAME:ARG%
				PRINTFORMW 꿀꺽 침을 삼키는 %CALLNAME:MASTER%
				PRINTFORML 서서히 가슴을 비비어 하기 만이는 손과\@ !LOCAL:2 ?  질구 # 항문 \@에 함네 붐비어진 손가락의 움직임이 빠르게 되어 간다
				PRINTFORMW 그리고, %CALLNAME:MASTER%의 이름을 부르면서, 전신을 진동시켜 절정 했다
			;심약계, 진면목계
			ELSEIF CFLAG:ARG:성격경향 == 1 || CFLAG:ARG:성격경향 == 4
				PRINTFORML 부끄러운 듯이, 가슴과\@ !LOCAL:2 ?  은밀한 곳 # 아누스 \@를 만지작거리는 %CALLNAME:ARG%
				PRINTFORML 지만, 희미하게 혈색이 좋아지는 피부에, 눈에 보여 날카로워지는 유두에, 열락이 틈새 보인다
				PRINTFORMW 서서히 소리가 커져, 조금씩 다리가 열어 간다
				PRINTFORML %CALLNAME:MASTER%의 시선을 눈치채지 않은 것인지, 소극적이지만 숨길 수 없는 정도로 다리가 열렸다
				PRINTFORML 주르륵, 애액이 늘어져 동시에, 꿈틀 허벅지가 경련한다
				PRINTFORMW 너무나 갑작스럽게 애액을 불면서, %CALLNAME:ARG%는 정상에 올라 채웠다
			;강경세계, 솔직함계, 그 외
			ELSEIF CFLAG:ARG:성격경향 == 2 || CFLAG:ARG:성격경향 == 3 || CFLAG:ARG:성격경향 == 5
				PRINTFORML 손을 댈까하고 접근하는 %CALLNAME:MASTER%에, 보일 뿐(만큼)과 다짐을 받아 오는 %CALLNAME:ARG%
				PRINTFORML 가슴을 만지작거리면서, 꿀꺽 꿀꺽등 강요하고 소리를 내,\@ !LOCAL:2 ?  은밀한 곳 # 아누스 \@를 만지작거리고 있다
				PRINTFORMW 이쪽의, 불만인 모습을 눈치챘는지, 한숨을 쉰%CALLNAME:ARG%가 크게 다리를 벌렸다
				PRINTFORML 뚝뚝흘러넘치는 애액과 팽팽하게 다 날카로워진 클리토리스는 선정적으로
				PRINTFORML 이쪽의 시선을 느끼고 있는 것인지, %CALLNAME:ARG%의 손가락의 움직임이 앞당겨져 간다
				PRINTFORMW 갑자기,\@ !LOCAL:2 ?  질강 # 항강 \@깊이 손가락을 찔러 붐비어, %CALLNAME:ARG%가 달했다
			ENDIF
		ENDCATCH
	ENDIF
	PRINTFORML 실룩실룩, 팔딱팔딱, 허벅지를 경련시켜, 전신을 진동시키는 %CALLNAME:ARG%
	IF CFLAG:MASTER:311 == CFLAG:ARG:311
		PRINTFORML %마스터는% 탈진해 버린 그녀에게 이불을 걸고 나서 침상에 도착했다
	ELSE
		PRINTFORML 축 늘어져 한 그 모습에, 과연 손을 대는 것이 꺼려진%마스터는% 그대로 돌아가기로 했다
		IF !CFLAG:ARG:노팬티 && EQUIP:ARG:하반신속옷2 && CFLAG:MASTER:데이트중 != MAIN_MAP
			PRINTFORML 모처럼이므로, 선물을 받아 돌아갑니까?
			CALL ASK_YN
			IF !RESULT
				PRINTFORML 돌아갈 때, %마스터는% %CALLNAME:ARG%의 %PANTSNAME(EQUIP:ARG:하반신속옷2, ARG)%를 주워 재산에 입금시켰다
				CALL PANTS_GET(ARG)
			ENDIF
		ENDIF
	ENDIF
	CFLAG:ARG:딸딸이들킴 = 3
	부디그대로호감도상승치 = ABL:ARG:친밀 * CFLAG:ARG:욕구불만도 / 250 * MIN(ABL:ARG:노출벽, 10)
	CALL CHANGE_CFLAG(2, ARG, 부디그대로호감도상승치)
	CALL CHANGE_CFLAG(4, ARG, 부디그대로호감도상승치 * -1 / 20)
	CALL JUEL_UP_SPECIAL(ARG,"치정","자위중독", 30,1)
	CALL JUEL_UP_SPECIAL(ARG,"욕정","자위중독", 30,1)
	CALL ADD_EXP_LESNIAN(CFLAG:ARG:욕구불만도 / 100, ARG, "PRINT")
	EXP:ARG:노출쾌락경험 += CFLAG:ARG:욕구불만도 / 200 * ABL:ARG:노출벽
	EXP:ARG:자위경험 +=  CFLAG:ARG:욕구불만도 / 200 * ABL:ARG:노출벽
	TIME += 27
ELSEIF RESULT == 2
	이루어무너뜨려필요치 = 껴안고필요치(ARG) - 150 + TALENT:ARG:수치심 * 30 - CFLAG:ARG:욕구불만도 / 20
	SIF  TALENT:ARG:자존심 > 0
		이루어무너뜨려필요치 += 50
	SIF CFLAG:ARG:기정사실 & 합의_우후후
		이루어무너뜨려필요치 -= 50
	SIF TALENT:ARG:담력 < 0
		이루어무너뜨려필요치 -= 200
	CALL SHOW_LOVEPOWER(CFLAG:ARG:합의판정, 50)
	CALL SHOW_REASONWALL(이루어무너뜨려필요치 , 50)
	$ GOOUT
	IF CFLAG:ARG:합의판정 <= 이루어무너뜨려필요치
		PRINTFORML %CALLNAME:ARG%는 알 수 없는 것을 마구 아우성쳐 %마스터를% 내쫓았다
		CALL KOJO_MESSAGE_SEND("EVENT", 26, ARG, 3,0)
		SIF !CFLAG:ARG:약점잡힘
			PRINTFORML %CALLNAME:ARG%의【약점】을 잡았다
		CALL GET_ANGRY(ARG, 1)
		CFLAG:ARG:딸딸이들킴 = 1
		CFLAG:ARG:약점잡힘 = 1
		TIME += 5
	ELSE
		CALL NASIKUZUSI(ARG, LOCAL:2)
		CFLAG:ARG:대주고말았다 = 1
	ENDIF
ENDIF
TARGET = TARGET보존

@NASIKUZUSI(ARG, ARG:1)
#DIM 전희1
;1=C 2=B 3=M
#DIM 전희2
#DIM 실전은어느쪽인가
#DIM 주입량
#DIM 기본주입량
#DIM 호감도상승량
#DIM 신뢰저하량
#DIM 회수
#DIM 경험증가수, 58
;0이라면 V, 1이라면 A
VARSET 경험증가수
CALL KOJO_MESSAGE_SEND("EVENT", 26, ARG, 4,0)
회수 = MAX(MIN(ABL:ARG:욕망 / 2, 5) + CFLAG:ARG:욕구불만도 / 200,1)
주입량 = 0
IF TALENT:ARG:키스미경험
	전희1 = 3
	경험증가수:4 += 3 + ABL:ARG:M감각 * 2
	경험증가수:25 = 경험증가수:4
	경험증가수:27 = 경험증가수:4
	EXP:MASTER:M경험 += 경험증가수:4
	EXP:MASTER:키스경험 += 경험증가수:4
	EXP:MASTER:펠라티오경험 += 경험증가수:4
ELSEIF !RAND:3
	전희1 = 3
	경험증가수:4 += 3 + ABL:ARG:M감각 * 2
	경험증가수:27 = 경험증가수:4
ELSEIF ABL:ARG:C감각 >= ABL:ARG:B감각
	전희1 = 1
	경험증가수:0 += RAND:(회수 * 2) 
	경험증가수:5 += 경험증가수:0 / 2
	경험증가수:10 += 경험증가수:0 / 2
ELSE
	전희1 = 2
	경험증가수:3 += RAND:(회수 * 2) 
	경험증가수:8 += 경험증가수:3 / 2
	경험증가수:10 += 경험증가수:0 / 2
ENDIF
전희2 = RAND:3
실전은어느쪽인가 = ARG:1

SIF 실전은어느쪽인가 == 1
	전희2 += 10
IF 실전은어느쪽인가
	기본주입량 = (ABL:ARG:애널 + 1) * 20 + BASE:MASTER:정력 /25
	경험증가수:21 += 회수
	경험증가수:17 += 회수
	경험증가수:10 += 회수
	경험증가수:7 += 2 * 회수
	경험증가수:2 += 회수 * 2
	EXP:MASTER:삽입경험 += 회수
	MAXBASE:MASTER:정력 += 회수*3;;정력증가
	EXP:MASTER:사정경험 += 회수
	EXP:MASTER:절정경험 += 회수
	
ELSE
	기본주입량 = (ABL:ARG:질 + 1) * 20 + BASE:MASTER:정력 / 25
	SIF TALENT:ARG:처녀 == 1
		기본주입량 += 100
	경험증가수:20 += 회수
	경험증가수:16 += 회수
	경험증가수:10 += 2 * 회수
	경험증가수:6 += 2 * 회수
	경험증가수:1 += 2 * 회수
	MAXBASE:MASTER:정력 += 회수*3;;정력증가
	EXP:MASTER:삽입경험 += 회수
	EXP:MASTER:사정경험 += 회수
	EXP:MASTER:절정경험 += 회수
ENDIF


FOR LOCAL, 1, 회수
	주입량 += 기본주입량 + RAND:(기본주입량)
NEXT

주입량 = MIN(주입량, BASE:MASTER:정력)


IF !실전은어느쪽인가
	CALL ADD_SAMEN_V(ARG, 주입량)
ELSE
	CALL ADD_SAMEN_A(ARG, 주입량)
ENDIF

;말체크
TRYCCALLFORM M_KOJO_EVENT_K{NO:ARG}_ONABARE_3(전희1, 실전은어느쪽인가, 회수, 주입량)
CATCH
	;이하 Default 지문
	PRINTFORML %마스터는% 동요하는 %CALLNAME:ARG%를 뒷전으로 뒤에서 문을 닫아 쑥쑥(거침없이 나섬)과 마구 들어가 앉으면(자),
	PRINTFORMW %CALLNAME:ARG%를 껴안아 무엇을 생각하면서 시라고 있었는지 신 있었다
	PRINTFORM 그리고 그녀가 바랬던 대로,

	SELECTCASE 전희1
		CASE 1
			PRINTDATA
				DATAFORM 클리토리스를 적마미
				DATAFORM 클리토리스를 손가락끝으로 튕겨
				DATAFORM 음핵을 손가락끝으로 반죽하고 돌리기
				DATAFORM 음핵을 빨아
			ENDDATA
		CASE 2
			PRINTDATA
				DATAFORM 유두를 손가락으로 적마미
				DATAFORM 가슴을 애무해
				DATAFORM 유방을 손으로 비비어 하기 만
				DATAFORM 유두를 혀로 굴리며
				DATAFORM 유두를 살짝 깨물고
			ENDDATA
		CASE 3
			PRINTDATA
				DATAFORM 머리카락을 쓰다듬고 있으니까
				DATAFORM 살며시
				DATAFORM 정열적으로
			ENDDATA
	ENDSELECT
	IF 전희1 == 3
		IF TALENT:ARG:키스미경험
			PRINTFORM 첫키스를 빼앗아
		ELSE
			PRINTFORM 입맞춤
		ENDIF
	ENDIF
	PRINT ,

	SELECTCASE 전희2
		CASE 0
			PRINTFORM 음순의 주위를 더듬어
		CASE 1
			PRINTFORM 질구에 얕게 손가락을 삽입해
		CASE 2
			PRINTFORM 질구를 혀로 후벼 파내어
		CASE 10
			PRINTFORM 엉덩이를 차분히 비비어 하기 만
		CASE 11
			PRINTFORM 항문의 주위를 더듬어
		CASE 12
			PRINTFORM 항문에 얕게 손가락을 삽입해
	ENDSELECT
	PRINT ,

	PRINTDATA
		DATAFORM 실컷 초조하게 해 주었다
		DATAFORM 철저하게 이나 키라고 했다
		DATAFORM 추잡한 말로 괴롭히고
	ENDDATA
	PRINTW
	PRINT 그리고
	PRINTDATA
		DATAFORM 엎드린 자세로 시키고 등 뒤로부터 짐승처럼 지워져
		DATAFORM 정면에서 얼싸안아
		DATAFORM 곧바로 돌려보네기의 자세를 받게 하고
		DATAFORM 정면에서 얼싸안아
	ENDDATA
	PRINTDATA
		DATAFORM 단 말을 속삭여
		DATAFORM 상냥하고
		DATAFORM 몹시 거칠고
		DATAFORM 끈적끈적
	ENDDATA

	IF TALENT:ARG:처녀 && !실전은어느쪽인가
		PRINTFORM 순결을 빼앗아,
	ELSE
		PRINTFORM {회수}도
		PRINTDATA
			DATAFORM 범해,
			DATAFORM 사귀어,
			DATAFORM 관철해,
		ENDDATA
	ENDIF
	PRINTFORMW \@실전은어느쪽인가 ?  애널# 질\@에{주입량}의 정액을 쏟았다……

	;이상 Default 지문
ENDCATCH

IF 전희1 == 3 
	SIF TALENT:MASTER:키스미경험
		CALL SET_FIRSTKISS(MASTER, ARG, 2)
	SIF TALENT:ARG:키스미경험
		CALL SET_FIRSTKISS(ARG, MASTER, 2, "흐르게 되는 대로")
ENDIF
IF TALENT:ARG:처녀 == -1 && !실전은어느쪽인가
	SETCOLOR 247,171,166
	PRINTFORMW 【무자각비처녀상실】
	TALENT:ARG:처녀 = 0
ENDIF
IF TALENT:ARG:처녀 == 1 && !실전은어느쪽인가
	SETCOLOR 247,171,166
	PRINTFORMW 【처녀상실】
	CALL SET_HISTORY_LOST_V(ARG, MASTER, "페니스", "자기 방", "흐르게 되는 대로")
ENDIF
IF !CFLAG:ARG:애널처녀상실기념일 && 실전은어느쪽인가
	SETCOLOR 247,171,166
	PRINTFORMW 【애널 처녀상실】
	CALL SET_HISTORY_LOST_A(ARG, MASTER, "페니스", "자기 방", "흐르게 되는 대로")
ENDIF
SETCOLOR C_YELLOW
FOR LOCAL, 0,58
	IF 경험증가수:LOCAL
		PRINTFORML %EXPNAME:LOCAL% ＋{경험증가수:LOCAL}
		EXP:ARG:LOCAL += 경험증가수:LOCAL
	ENDIF
NEXT
RESETCOLOR
CALL JUEL_UP_SPECIAL(ARG,"욕정","욕망", 20,1)
SELECTCASE 전희1
;C
	CASE 1
		CALL JUEL_UP_SPECIAL(ARG,"쾌C","C감각", 10,1)
;B
	CASE 2
		CALL JUEL_UP_SPECIAL(ARG,"쾌B","B감각", 10,1)
;M
	CASE 3
		CALL JUEL_UP_SPECIAL(ARG,"쾌M","M감각", 20,1)
ENDSELECT
IF 실전은어느쪽인가
	CALL JUEL_UP_SPECIAL(ARG,"쾌A","A감각", 15,1)
ELSEIF TCVAR:ARG:파과
	CALL JUEL_UP_SPECIAL(ARG,"쾌V","V감각", 100,1)
ELSE
	CALL JUEL_UP_SPECIAL(ARG,"쾌V","V감각", 15,1)
ENDIF
CALL ADD_EXP_LESNIAN(회수 * 5, ARG, "PRINT")
PRINTFORMW …
PRINTFORMW ……
PRINTFORMW ………

CALL KOJO_MESSAGE_SEND("EVENT", 26, ARG, 5,0)
TIME += 회수 * 20
EXP:MASTER:청소경험 ++
DOWNBASE:MASTER:체력 = 회수 * 100
DOWNBASE:MASTER:기력 = 회수 * 150
호감도상승량 = 회수 * ABL:ARG:친밀 * 5
신뢰저하량 = 호감도상승량 / 5
SIF TALENT:ARG:사모
	신뢰저하량 /= 2
SIF TALENT:ARG:섹프
	신뢰저하량 /= 2
SIF TALENT:ARG:연모
	신뢰저하량 /= 4

CALL CHANGE_CFLAG(2, ARG, 호감도상승량)
CALL CHANGE_CFLAG(4, ARG, 신뢰저하량 * -1)
CFLAG:ARG:욕구불만도 = 0
TCVAR:ARG:파과 = 0

IF TALENT:ARG:연모 && CFLAG:MASTER:데이트중 != MAIN_MAP
	PRINTFORML 행위를 끝낸%마스터는% 일어서려고 했지만, %CALLNAME:ARG%는 아직 함께 있었으면 좋은 것 같다……
	CALL ASK_YN("그런데도 돌아가는","묵어 갈까")
	IF !RESULT
		GOTO BYEBYE
	ELSE
		CALL KOJO_MESSAGE_SEND("EVENT", 26, ARG, 6)
		;초대 플래그에 ARG를 대입
		CFLAG:ARG:초대 = ARG
		CFLAG:MASTER:초대 = ARG
		;초기위치를 OMANEKIBEYA()=98로 한다
		CFLAG:ARG:초기위치 = OMANEKIBEYA()
		;이동시킨다
		CFLAG:ARG:현재위치 = OMANEKIBEYA()
		CFLAG:MASTER:현재위치 = OMANEKIBEYA()
		IF NEMUKE() < 720 && BASE
			PRINTFORML %마스터는% 오늘 하루%CALLNAME:ARG%로 보내기로 했다
			PRINTFORML 당분간 함께 자, 일어나, 청소를 해, 러브러브 해, 식사를 만들어 먹고,
			PRINTFORMW 또 러브러브 해, 섹스 해 잤다
			VARSET 경험증가수
			IF 실전은어느쪽인가
				주입량 = ABL:ARG:애널 * MIN(ABL:ARG:친밀, 15) * 10
				경험증가수:21 += MIN(ABL:ARG:친밀, 15)
				경험증가수:17 += MIN(ABL:ARG:친밀, 15)
				경험증가수:7 += MIN(ABL:ARG:친밀, 15) * 2
				경험증가수:2 += MIN(ABL:ARG:친밀, 15) * 2
			ELSE
				주입량 = ABL:ARG:질 * MIN(ABL:ARG:친밀, 15) * 10
				경험증가수:20 += MIN(ABL:ARG:친밀, 15)
				경험증가수:16 += MIN(ABL:ARG:친밀, 15)
				경험증가수:6 += 2 * MIN(ABL:ARG:친밀, 15)
				경험증가수:1 += 2 * MIN(ABL:ARG:친밀, 15)
			ENDIF
			경험증가수:41 =  MIN(ABL:ARG:친밀, 15) * 2
			주입량 = MIN(주입량, BASE:MASTER:정력)
			CALL JUEL_UP_SPECIAL(LOCAL,"욕정","욕망", 15)
			IF !실전은어느쪽인가
				CALL ADD_SAMEN_V(ARG, 주입량)
				CALL JUEL_UP_SPECIAL(ARG,"쾌V","V감각", 10,1)
			ELSE
				CALL ADD_SAMEN_A(ARG, 주입량)
				CALL JUEL_UP_SPECIAL(ARG,"쾌A","A감각", 10,1)
			ENDIF
			FOR LOCAL, 0,58
				IF 경험증가수:LOCAL
					PRINTFORML %EXPNAME:LOCAL% ＋{경험증가수:LOCAL}
					EXP:ARG:LOCAL += 경험증가수:LOCAL
				ENDIF
			NEXT
			CALL ADD_EXP_LESNIAN(ABL:ARG:친밀 * 2, ARG, "PRINT")
			호감도상승량 = ABL:ARG:친밀 * 10
			EXP:MASTER:청소경험 ++
			EXP:MASTER:요리경험 ++
			TIME += 720 - NEMUKE()
		ELSE
			PRINTFORML 졸려졌으므로, 그대로 %CALLNAME:ARG%과 함께 잤다
			CALL ADD_EXP_LESNIAN(ABL:ARG:친밀, ARG, "PRINT")
			호감도상승량 = ABL:ARG:친밀 * 5
		ENDIF
		CALL CHANGE_CFLAG(2, ARG, 호감도상승량)
		CFLAG:MASTER:취침시간 = DAY * 1440 + TIME
		BEGIN AFTERTRAIN
		RETURN 1
	ENDIF
ENDIF

;TW당신은 정리하지 않고서는  견딜 수 없다
$BYEBYE
PRINTFORM 뒤처리를 한 %마스터는%,\@ 실전은어느쪽인가?  항문# 비렬\@로부터\@TCVAR:ARG:파과 ?  파과피  섞인# \@백탁을 흘러넘치게 해 보 차는 %CALLNAME:ARG%에,

IF ABL:ARG:정액중독 >= (3 + 실전은어느쪽인가 * 2)
	PRINTFORML 청소 해 주었기 때문에 대신에 여기를 깨끗이 해라고 말해 입가에\@TCVAR:ARG:파과 ? 그녀의 순결을 가라앉힌지 얼마 안된#국물 투성이의 \@페니스를 내밀었다
	PRINTFORMW %CALLNAME:ARG%는 멍하니 한 표정으로 정액을 빨고 취하고 있다……
	SETCOLOR C_YELLOW
	PRINTFORML 정음경험 ＋{ABL:ARG:정액중독}
	EXP:ARG:정음경험 += ABL:ARG:정액중독
	RESETCOLOR
	CALL JUEL_UP_SPECIAL(ARG,"굴복","정액중독", 5,1)
	PRINTFORM 완전히 만족한 %마스터는%,
ENDIF

PRINTFORMW \@ CFLAG:MASTER:데이트중 == MAIN_MAP?  휴업(휴가)# 굉장히 좋았다, 다음은 %GET_MAPNAME(MAIN_MAP)%로 하자\@라고 해\@ CFLAG:MASTER:311 == CFLAG:ARG:311 ?  잠에 오른# 떠났다\@
CFLAG:ARG:딸딸이들킴 = 2
IF !CFLAG:ARG:노팬티 && CFLAG:MASTER:311 != CFLAG:ARG:311 && CFLAG:MASTER:데이트중 != MAIN_MAP && PANTSNAME(EQUIP:ARG:하반신속옷2, ARG) != "노팬티"
	PRINTFORML 모처럼이므로, 선물을 받아 돌아갑니까?
	CALL ASK_YN
	IF !RESULT
		PRINTFORML 돌아갈 때, %마스터는% %CALLNAME:ARG%의 %PANTSNAME(EQUIP:ARG:하반신속옷2, ARG)%를 주워 재산에 입금시켰다
		CALL PANTS_GET(ARG)
	ENDIF
ENDIF
