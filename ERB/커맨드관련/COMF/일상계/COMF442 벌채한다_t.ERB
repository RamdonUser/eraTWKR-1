;벌채한다
@COM442
#DIM 채취수

채취수 = 1 + ABL:MASTER:전투능력
IF TARGET && !FLAG:70
	채취수 += ABL:TARGET:전투능력
	DOWNBASE:체력 = 100
	DOWNBASE:기력 = 100
ENDIF

IF FLAG:70
	BASE:MASTER:TSP -= 200
ELSE
	DOWNBASE:MASTER:체력 = 100
	DOWNBASE:MASTER:기력 = 100
	TIME += 60
ENDIF

PRINTFORMW %마스터는% 나무를 온다―
PRINTFORMW 헤이 헤이 호-
PRINTFORMW 헤이 헤이 호-
PRINTFORMW …
PRINTFORMW ……
PRINTFORMW 목재를{채취수}손에 넣었다

ITEM:목재 += 채취수

SELECTCASE RAND:20
	CASE 0 TO 4
		LOCAL = 377
	CASE 5
		LOCAL = 4
	CASEELSE
		LOCAL = 0
ENDSELECT
IF LOCAL
	PRINTFORMW 나무에 무엇이지는 있다…
	PRINTFORMW %ITEMNAME:LOCAL%를 손에 넣었다
	ITEM:LOCAL ++
ENDIF

RETURN 1

@COM_ABLE442
;실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(419)
	RETURN RESULT
SIF CFLAG:우후후 == 2
	RETURN 0
SIF !벌채가능()
	RETURN 0
RETURN 1

@벌채가능()
#FUNCTION
IF !IN_HOME(CFLAG:MASTER:현재위치)
	SELECTCASE CFLAG:MASTER:현재위치
		CASE 숲의입구, 고목의큰나무, 산록의수해
			RETURNF 1
	ENDSELECT
ENDIF
RETURNF 0