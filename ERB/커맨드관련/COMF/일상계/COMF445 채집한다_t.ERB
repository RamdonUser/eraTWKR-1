;채집한다
@COM445
#DIM 채집물
#DIM 채집파워
#DIM 채집파워M
#DIM 채집파워T
#DIM 토지의은혜
#DIM CHARA

;명백한 사람의 부지에서의 무단 채집은 그만둔다
SELECTCASE CFLAG:MASTER:현재위치
	CASE 태양의밭, 438
		IF ABL:[[유카]]:친밀 < 5
			DRAWLINE
			PRINTFORML 여기서 무단으로 채집을 하는 것은 매우 위험한 생각이 든다…
			RETURN -1
		ENDIF
	;천계, 도원
	CASE 천계, 833
		IF ABL:[[텐시]]:친밀 < 5
			DRAWLINE
			PRINTFORML 여기서 무단으로 채집을 하는 것은 곤란한 생각이 든다…
			RETURN -1
		ENDIF
ENDSELECT

채집물 = 0
채집파워M = 0
채집파워T = 0
LOCAL:4 = 0

;참가자 판정
IF FLAG:데이트상대 && !FLAG:70
	CHARA = FLAG:데이트상대
ELSEIF TARGET && ABL:TARGET:친밀 >= 5 && !FLAG:70
	CHARA = TARGET
ELSE
	CHARA = 0
ENDIF

;토지의 은혜. 일일로-25～+25의 사이를 랜덤으로 부동. 낚시를 참고에.
토지의은혜 = FLAG:일일이벤트 / 20 - 25

SIF ITEM:스와코의선물
	토지의은혜 += 10
	
;채집 파워 설정
채집파워M = 10 + TALENT:MASTER:채집Lv * 10 + ABL:MASTER:교양 * 5 + TFLAG:행운보정 * 3
SIF ITEM:108
	채집파워M += 10

SIF CHARA && !FLAG:70
	채집파워T = 10 + TALENT:CHARA:채집Lv * 10 + ABL:CHARA:교양 * 5
;참가자로 가장 파워가 높은 것을 기준에,1/2한 이제(이미/벌써/더는/정말) 1명의 파워를 더한다
IF 채집파워M >= 채집파워T
	채집파워 = 채집파워M + (채집파워T / 2)
ELSE
	채집파워 = 채집파워T + (채집파워M / 2)
ENDIF


REPEAT 5
	REUSELASTLINE  \@ CHARA ? %조사처리(CALLNAME:CHARA,"와")% 함께 #  \@채집중.%"." * COUNT%
	TWAIT 150, 1
REND
;디버그용
;LOCAL:6 = GATHERING_SPOT(CFLAG:MASTER:현재 위치)
;PRINTFORML {LOCAL:6}

$TREASURSARCH

;채집물의 선출（LOCAL:1 개시치, LOCAL:2 종료치, LOCAL:3늘리는 값）
;랜덤으로 검색 스타트 위치를 비켜 놓거나 검색의 방향을 바꾸거나 한다
SELECTCASE RAND:5
	CASE 0
		LOCAL:1 = 600 + RAND:40
		LOCAL:2 = 707
		LOCAL:3 = 1
	CASE 1
		LOCAL:1 = 706
		LOCAL:2 = 599
		LOCAL:3 = -1
	CASE 2
		LOCAL:1 = 640 - RAND:40
		LOCAL:2 = 599
		LOCAL:3 = -1
	CASEELSE
		LOCAL:1 = 600 + (5 * (RAND:9))
		LOCAL:2 = 707
		LOCAL:3 = 1
ENDSELECT

FOR LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3
	;アイテムが無名でなく、出現条件を満たし、所有数上限でない
	SIF ITEMNAME:(LOCAL:0) == ""
		CONTINUE
	SIF ITEM:(LOCAL:0) == 99
		CONTINUE
	IF ITEMNAME:(LOCAL:0) != "" && GATHERING_PRODUCT(LOCAL:0, GATHERING_SPOT(CFLAG:MASTER:현재위치)) >= 1 && ITEM:(LOCAL:0) != 99 && GATHERING_SEASON(LOCAL:0)
		LOCAL:5 = ITEMPRICE:(LOCAL:0) + 1000
		;채집 파워＋보정치가 √ 아이템 비용+1000을 웃돌면 입수
		IF 채집파워 + 토지의은혜 >= SQRT(LOCAL:5)
			채집물 = LOCAL:0
			;가격 10000을 넘는 희소 아이템은 1개 밖에 손에 들어 오지 않는, 그것 이하의 가격대는 채집 파워에 응해 복수 입수가 가능하게
			SELECTCASE LOCAL:5
				CASE 0 TO 2000
					TFLAG:194 = MAX((RAND:(SQRT(채집파워)) - 1), 1)
				CASE 2001 TO 4000
					TFLAG:194 = MAX((RAND:(SQRT(채집파워)) - 2), 1)
				CASE 4001 TO 7000
					TFLAG:194 = MAX((RAND:(SQRT(채집파워)) - 3), 1)
				CASE 7001 TO 10999
					TFLAG:194 = MAX((RAND:(SQRT(채집파워)) - 4), 1)
				CASEELSE
					TFLAG:194 = 1
			ENDSELECT
			BREAK
		ENDIF
	ENDIF
NEXT
;시행 회수를 1회늘린다
LOCAL:4 ++
;채집물이 마음에 들지 않는 경우는 당겨 수선
IF 채집물 != 0
	SETCOLOR 0x00FF00
	PRINTFORM %ITEMNAME:(채집물)%
	RESETCOLOR
	PRINTFORML %조사만처리(ITEMNAME:(채집물),"가")% 발견되었다. 채집합니까?
	CALL ASK_YN
	IF !RESULT
		CLEARLINE 4
	ELSE
		CLEARLINE 4
		채집물 = 0
		TFLAG:194 = 0
		REPEAT 5
			REUSELASTLINE  \@ CHARA ? %조사처리(CALLNAME:CHARA,"와")% 함께 #  \@채집중.%"." * COUNT%
			TWAIT 150, 1
		REND
	ENDIF
ENDIF

;시행 회수 3회 이하로 채집물이 확보되어 있지 않은 경우는 루프
SIF LOCAL:4 <= 3 && 채집물 == 0
	GOTO TREASURSARCH

; 아이템 입수 처리 99개 이상이 되지 않도록 한다
IF 채집물 != 0
	IF ITEM:(채집물) + TFLAG:194 > 99
		ITEM:(채집물) = 99
	ELSE
		ITEM:(채집물) += TFLAG:194
	ENDIF
ENDIF

;기본경험
EXP:MASTER:채집경험 ++
SIF CHARA
	EXP:CHARA:채집경험 ++

SIF ITEM:108 && !RAND:2
	EXP:MASTER:채집경험 ++

SIF ABL:MASTER:교양 <= 2
	EXP:MASTER:학습경험 ++

;능력차이로 가르쳐 받는·가르쳐 준다
IF 채집파워M >= 채집파워T && CHARA
	EXP:CHARA:채집경험 ++
ELSEIF CHARA
	EXP:MASTER:채집경험 ++
ENDIF
IF ABL:MASTER:교양 >=  ABL:CHARA:교양 && CHARA
	EXP:CHARA:학습경험 ++
ELSEIF CHARA
	EXP:MASTER:학습경험 ++
ENDIF

;데이트 경험
IF CHK_DATENOW(CFLAG:CHARA:데이트중) && CHARA > 0
	EXP:MASTER:데이트경험 ++
	EXP:CHARA:데이트경험 ++
ENDIF

;시간 정지시는 TSP감
IF FLAG:70
	BASE:MASTER:TSP -= 120
ELSE
	;시간 경과
	TIME += 60
	;체력 감소는 전투 능력에 의해 감소 억제
	DOWNBASE:MASTER:체력 = 180 - (ABL:MASTER:전투능력 * 15)
	DOWNBASE:MASTER:기력 = 150 
ENDIF

;참가자의 소스, 동일 커맨드 제한을 받지 않기 때문에 소극적으로, 발견되지 않으면 반감
IF CHARA > 0
	;고정으로 획득하는 소스
	SOURCE:CHARA:환락 = 300
	SOURCE:CHARA:사랑 = 50
	
	;ABL:순종을 본다
	IF ABL:순종 <= 1
		SOURCE:CHARA:환락 += (ABL:순종 * 40)
		SOURCE:CHARA:사랑 += (ABL:친밀 * 20)
	ELSEIF ABL:순종 <= 3
		SOURCE:CHARA:환락 += 200 + (ABL:순종 * 40)
		SOURCE:CHARA:사랑 += 50 + (ABL:친밀 * 30)
	ELSEIF ABL:순종 <= 5
		SOURCE:CHARA:환락 += 500 + (ABL:순종 * 40)
		SOURCE:CHARA:사랑 += 200 + (ABL:친밀 * 30)
	ELSEIF ABL:순종 <= 8
		SOURCE:CHARA:환락 += 750 + (ABL:순종 * 60)
		SOURCE:CHARA:사랑 += 300 + (ABL:친밀 * 40)
	ELSEIF ABL:순종 <= 11
		SOURCE:CHARA:환락 += 1000 + (ABL:순종 * 60)
		SOURCE:CHARA:사랑 += 600 + (ABL:친밀 * 40)
	ELSE
		SOURCE:CHARA:환락 += 1600 + (ABL:순종 * 30)
		SOURCE:CHARA:사랑 += 1000 + (ABL:친밀 * 20)
	ENDIF
	SIF CHARA != FLAG:데이트상대 
		SOURCE:CHARA:사랑 = 0
	
	;호감도는 보지 않는다
	
	SOURCE:CHARA:수동 = 200 + 100 * ABL:순종
	SOURCE:CHARA:정복 = 200 + 100 * ABL:새드끼
	
	;채집 성과를 보는, 아이템 가격에 응해 우선 설정
	IF 채집물
		SELECTCASE ITEMPRICE:(채집물)
			CASE 1 TO 499
				TIMES SOURCE:CHARA:환락 , 1.00
				TIMES SOURCE:CHARA:수동 , 1.00
				TIMES SOURCE:CHARA:정복 , 1.00
				SOURCE:CHARA:달성 = 80
			CASE 500 TO 999
				TIMES SOURCE:CHARA:환락 , 1.10
				TIMES SOURCE:CHARA:수동 , 1.10
				TIMES SOURCE:CHARA:정복 , 1.10
				SOURCE:CHARA:달성 = 115
			CASE 1000 TO 2999
				TIMES SOURCE:CHARA:환락 , 1.25
				TIMES SOURCE:CHARA:수동 , 1.25
				TIMES SOURCE:CHARA:정복 , 1.15
				SOURCE:CHARA:달성 = 125
			CASE 3000 TO 5999
				TIMES SOURCE:CHARA:환락 , 1.50
				TIMES SOURCE:CHARA:수동 , 1.50
				TIMES SOURCE:CHARA:정복 , 1.25
				SOURCE:CHARA:달성 = 150
			CASE 6000 TO 9999
				TIMES SOURCE:CHARA:환락 , 1.75
				TIMES SOURCE:CHARA:수동 , 1.75
				TIMES SOURCE:CHARA:정복 , 1.50
				SOURCE:CHARA:달성 = 200
			CASE 10000 TO 14999
				TIMES SOURCE:CHARA:환락 , 2.00
				TIMES SOURCE:CHARA:수동 , 2.00
				TIMES SOURCE:CHARA:정복 , 1.75
				SOURCE:CHARA:달성 = 300
			CASEELSE
				TIMES SOURCE:CHARA:환락 , 2.25
				TIMES SOURCE:CHARA:수동 , 2.25
				TIMES SOURCE:CHARA:정복 , 2.00
				SOURCE:CHARA:달성 = 350
		ENDSELECT
	;발견되지 않는다
	ELSE
		TIMES SOURCE:CHARA:환락 , 0.50
		TIMES SOURCE:CHARA:수동 , 0.50
		TIMES SOURCE:CHARA:정복 , 0.50
	ENDIF
	;채집물의 캐릭터에게 응한 고유 반응（덧쓰기）
	SELECTCASE 채집물
		;개구리
		CASE 706
		;치르노
			IF CHARA == [[치르노]]
				TIMES SOURCE:CHARA:환락 , 2.00
				TIMES SOURCE:CHARA:수동 , 2.00
				TIMES SOURCE:CHARA:정복 , 2.00
			;스와코
			ELSEIF CHARA == [[스와코]]
				TIMES SOURCE:CHARA:환락 , 0.50
				TIMES SOURCE:CHARA:수동 , 0.50
				TIMES SOURCE:CHARA:정복 , 0.50
			ELSE
				TIMES SOURCE:CHARA:환락 , 1.25
				TIMES SOURCE:CHARA:수동 , 1.00
				TIMES SOURCE:CHARA:정복 , 1.00
			ENDIF
			SOURCE:CHARA:달성 = 110
	ENDSELECT
ENDIF

;플래그 인도
TFLAG:SELECTCOM분기 = 채집물

RETURN 1

;-------------------------------------------------
;실행 가능 판정
;-------------------------------------------------
;채집한다
@COM_ABLE445
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(445)
	RETURN RESULT
;채집할 수 있는 장소이다
SIF !GATHERING_SPOT(CFLAG:MASTER:현재위치)
	RETURN 0
;데이트중은 데이트 상대와 만
SIF FLAG:데이트상대 && TARGET != FLAG:데이트상대
	RETURN 0
;수면중
SIF CFLAG:MASTER:수면
	RETURN 0
SIF CFLAG:우후후
	RETURN 0
;체력·기력 부족
SIF BASE:MASTER:기력 < 650 || BASE:MASTER:체력 < 680
	RETURN 0
;시간 정지중도 가능（가짜）
;SIF FLAG:70
;	RETURN 0
RETURN 1

;-----------------------------------------------------------
;현재 위치로부터 채집 스팟을 산출하는 함수
;ARG		현재 위치
;RETURN		0 해당 없음	1～30숲, 초원	31～60 수계		61～ 특수
;COMMENT	장소를 추가했을 경우, CASE를 변경의 일
;-----------------------------------------------------------
@GATHERING_SPOT(ARG)
#FUNCTION
IF !AT_HOME(MASTER)
;서브 맵 따위에서의 판정
	SELECTCASE ARG
		CASE 수호신의숲
			SIF MAIN_MAP != 0
				RETURNF 1
		CASE 사각의죽림		;430메인 맵에서는 「지하실」을 지정하기 위해(때문에) 제외
			SIF MAIN_MAP != 4
				RETURNF 2
		CASE 무명의언덕
			RETURNF 3
		CASE 태양의밭
			RETURNF 4
		CASE 재고의길
			RETURNF 5
		CASE 고목의큰나무		;530
			SIF MAIN_MAP != 5
				RETURNF 6
		CASE 630	;삼도천～명계에 있어서의 마요히가메인맙 실장시에는 고쳐 쓸 필요 있을지도
			RETURNF 7
		CASE 740	;요괴의산·산록에 있어서의 마요히가
			SIF MAIN_MAP != 7
				RETURNF 7
		CASE 피안
			RETURNF 8
		CASE 산록의수해
			SIF MAIN_MAP != 7
				RETURNF 11
		CASE 절경의언덕
			SIF MAIN_MAP != 8
			RETURNF 12
		CASE 천계
			RETURNF 13
		CASE 복숭아숲～모래사장
			RETURNF 14
		CASE 안개의호수
			SIF MAIN_MAP != 3
				RETURNF 31
		CASE 삼도천
			RETURNF 32
		CASE 현무의늪
			SIF MAIN_MAP != 7
				RETURNF 33
		CASE 큰두꺼비의연못
			SIF MAIN_MAP != 7
				RETURNF 34
		CASE 구천의폭포
			SIF MAIN_MAP != 8
				RETURNF 35
		CASE 산의호수
			SIF MAIN_MAP != 8
				RETURNF 36
		CASE 지옥의깊은길
			SIF MAIN_MAP != 9
				RETURNF 37
		CASE 묘지
			RETURNF 61
		CASE 무연총
			RETURNF 62
		CASE 간헐천지하센터
			RETURNF 64
		CASE 작열지옥터
			RETURNF 65
		CASE 회안통로
			SIF MAIN_MAP != 9
				RETURNF 66
		CASE 고요의바다
			RETURNF 67
	ENDSELECT
ELSE
	;メインマップでの判定
	SELECTCASE MAIN_MAP
		CASE 0
			;博霊神社：鎮守の森
			SIF ARG == 25
				RETURNF 1
		CASE 1
			;명련사:묘지
			SIF ARG == 121
				RETURNF 61
		CASE 3
			;홍마관:호숫가
			SIF ARG == 343
				RETURNF 31
		CASE 4
			;영원정:사각의죽림, 무명의언덕, 태양의 밭
			SELECTCASE ARG
				CASE 433
					RETURNF 2
				CASE 436
					RETURNF 3
				CASE 438
					RETURNF 4
			ENDSELECT
		CASE 5
			;마법의숲:무연총, 버섯 군생지, 재고의길, 고목의 큰나무
			SELECTCASE ARG
				CASE 506
					RETURNF 62
				CASE 507
					RETURNF 63
				CASE 509
					RETURNF 5
				CASE 540
					RETURNF 6
			ENDSELECT
		CASE 7
			;요괴의산 산록:늪에 계속되는 길, 현무의 늪, 중턱, 대두꺼비의 연못
			SELECTCASE ARG
				CASE 702
					RETURNF 9
				CASE 703
					RETURNF 33
				CASE 705
					RETURNF 10
				CASE 708
					RETURNF 34
			ENDSELECT
		CASE 8
			;요괴의산 산정:정상, 폭포의 뒤, 호숫가, 도원
			SELECTCASE ARG
				CASE 805
					RETURNF 12
				CASE 806
					RETURNF 35
				CASE 814
					RETURNF 36
				CASE 833
					RETURNF 13
			ENDSELECT
		CASE 9
			;지저:작열 지옥자취, 강가, 세로구멍의 바닥
			SELECTCASE ARG
				CASE 928
					RETURNF 65
				CASE 945
					RETURNF 37
				CASE 948
					RETURNF 66
			ENDSELECT
	ENDSELECT
ENDIF
RETURNF 0

;-------------------------------------------------
;그 채집 스팟에서의 채집물의 출현 판정을 하는 함수
; 아이템 번호 ARG:0
;GATHERING_SPOT(ARG)의 돌아가 값ARG:1（채집 스팟）  의
;출현 가부를 숫자로 돌려주는 출현==1비출현==0
; 아이템을 추가했을 경우는, GROUPMATCH내에 각각 덧붙여 씀
;-------------------------------------------------
@GATHERING_PRODUCT(ARG:0, ARG:1)
#FUNCTION

SELECTCASE ARG:1
	;고장을 지키는 신 숲, 요정의 큰 나무
	CASE 1
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 607, 608, 611, 612, 616, 619, 621, 622, 626, 627, 628, 636)
			RETURNF 1
	;경사각의 대나무숲
	CASE 2
		SIF GROUPMATCH(ARG:0, 607, 608, 611, 612, 613, 618, 619, 629, 635, 640)
			RETURNF 1
	;무명의 언덕
	CASE 3
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 603, 612, 616, 620, 626, 627, 631, 636, 638)
			RETURNF 1
	;태양의 밭
	CASE 4
		SIF GROUPMATCH(ARG:0, 612, 613, 614, 615, 617, 618, 619, 620, 623, 624, 626, 627)
			RETURNF 1
	;재고의 길
	CASE 5
		SIF GROUPMATCH(ARG:0, 612, 614, 619, 624, 631, 632, 638)
			RETURNF 1
	;고목의 큰 나무
	CASE 6
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 603, 604, 605, 607, 608, 611, 612, 616, 618, 621, 622, 623, 625, 626, 627, 628)
			RETURNF 1
	;마요히가
	CASE 7
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 603, 607, 608, 611, 612, 616, 617, 619, 621, 622, 626, 627, 628)
			RETURNF 1
	;피안
	CASE 8
		SIF GROUPMATCH(ARG:0, 600, 601, 607, 608, 612, 614, 620, 624, 638)
			RETURNF 1
	;산길·늪에 계속되는 길
	CASE 9
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 607, 608, 611, 612, 616, 617, 621, 622, 626, 627, 628, 636)
			RETURNF 1
	;산길·중턱
	CASE 10
		SIF GROUPMATCH(ARG:0, 611, 613, 616, 619, 621, 622, 623, 633)
			RETURNF 1
	;산기슭의 수해
	CASE 11
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 607, 608, 611, 612, 613, 616, 617, 619, 621, 622, 623, 626, 627, 628, 633)
			RETURNF 1
	;절경의 언덕, 정상
	CASE 12
		SIF GROUPMATCH(ARG:0, 602, 603, 607, 608, 611, 612, 615, 616, 619, 621, 622, 625, 626, 627, 628)
			RETURNF 1
	;천계, 도원
	CASE 13
		SIF GROUPMATCH(ARG:0, 615, 623, 630)
			RETURNF 1
	;도림～모래 사장
	CASE 14
		SIF GROUPMATCH(ARG:0, 607, 608, 613, 615, 617, 618, 624, 625, 630)
			RETURNF 1
	;안개호, 호숫가(홍마관)
	CASE 31
		SIF GROUPMATCH(ARG:0, 606, 607, 608, 609, 611, 612, 617, 624, 633, 706)
			RETURNF 1
	;삼도천
	CASE 32
		SIF GROUPMATCH(ARG:0, 607, 608, 612, 614, 631, 638, 706)
			RETURNF 1
	;현무의 늪
	CASE 33
		SIF GROUPMATCH(ARG:0, 606, 607, 609, 611, 612, 618, 635, 706)
			RETURNF 1
	;대두꺼비의 연못
	CASE 34
		SIF GROUPMATCH(ARG:0, 604, 612, 620, 631, 636, 706)
			RETURNF 1
	;구천의 폭포, 폭포의 뒤
	CASE 35
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 606, 609, 611, 613, 631, 634, 706)
			RETURNF 1
	;산의 호수, 호숫가（요괴의산 산정）
	CASE 36
		SIF GROUPMATCH(ARG:0, 606, 607, 609, 611, 612, 618, 632, 706)
			RETURNF 1
	;지옥의 후카미치, 강가
	CASE 37
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 604, 606, 609, 620, 631, 633, 636, 706)
			RETURNF 1
	;묘지
	CASE 61
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 604, 614, 620, 631, 636, 638)
			RETURNF 1
	;무연총
	CASE 62
		SIF GROUPMATCH(ARG:0, 604, 606, 609, 631, 634, 635, 636, 638)
			RETURNF 1
	;버섯 군생지
	CASE 63
		SIF GROUPMATCH(ARG:0, 600, 601, 602, 603, 604, 605, 605, 629)
			RETURNF 1
	;간헐천 지하 센터
	CASE 64
		SIF GROUPMATCH(ARG:0, 632, 634, 637)
			RETURNF 1
	;작열 지옥자취
	CASE 65
		SIF GROUPMATCH(ARG:0, 631, 634, 635, 637, 638)
			RETURNF 1
	;환상 바람구멍, 세로구멍의 바닥
	CASE 66
		SIF GROUPMATCH(ARG:0, 602, 604, 631, 632, )
			RETURNF 1
	;정인가의 바다
	CASE 67
		SIF GROUPMATCH(ARG:0, 639)
			RETURNF 1
ENDSELECT
RETURNF 0

;-------------------------------------------------
;채집물의 계절마다의 출현 판정
; 아이템 번호 ARG:0의 계절마다의
;출현 가부를 숫자로 돌려주는 함수 출현==1비출현==0
; 아이템을 추가했을 경우는, CASE에 덧붙여 씀
;-------------------------------------------------
@GATHERING_SEASON(ARG:0)
#FUNCTION

SELECTCASE DAY:2
	;봄:
	CASE 1
		SELECTCASE ARG:0
			CASE 600 TO 602, 604 TO 608, 610 TO 613, 616, 618 TO 627, 629, 631 TO 640, 706
				RETURNF 1
		ENDSELECT
	;여름
	CASE 2
		SELECTCASE ARG:0
			CASE 600 TO 601, 603, 606 TO 608, 612 TO 613, 618 TO 627, 630 TO 640, 706
				RETURNF 1
		ENDSELECT
	;가을
	CASE 3
		SELECTCASE ARG:0
			CASE 600 TO 608, 611 TO 614, 616 TO 623, 625 TO 628, 631 TO 640, 706
				RETURNF 1
		ENDSELECT
	;동
	CASE 4
		;천계, 지저, 달과 버섯 군생지는 여름으로 간주해 채집 가능
		IF GROUPMATCH(GATHERING_SPOT(CFLAG:MASTER:현재위치), 13, 14, 37, 63, 64, 65, 66, 67)
			SELECTCASE ARG:0
				CASE 600 TO 601, 603, 606 TO 608, 612 TO 613, 618 TO 627, 630 TO 640, 706
					RETURNF 1
			ENDSELECT
		ELSE
			SELECTCASE ARG:0
				CASE 606, 609, 615, 620, 631 TO 635, 637 TO 640
					RETURNF 1
			ENDSELECT
		ENDIF
ENDSELECT
RETURNF 0


@SHOW_GATHERING_LIST
#DIM PLACE
PRINTFORML 採集場所一覧
CALL COLORMESSAGE(@"中級素材　",C_YELLOW)
CALL COLORMESSAGE(@"高級素材　",C_P_GREEN)
CALL COLORMESSAGE(@"最高級素材",C_AQUA)
PRINTFORML 
DRAWLINE
PRINTFORMLC [1] 鎮守の森・妖精の大樹
PRINTFORMLC [2] 斜角の竹林
PRINTFORMLC [3] 無名の丘
PRINTFORMLC [4] 太陽の畑
PRINTFORMLC [5] 再思の道
PRINTFORMLC [6] 古木の大樹
PRINTFORMLC [7] マヨヒガ
PRINTFORMLC [8] 彼岸
PRINTFORMLC [9] 山道・沢へ続く道
PRINTFORMLC [10] 山道・中腹
PRINTFORMLC [11] 麓の樹海
PRINTFORMLC [12] 絶景の丘、頂上
PRINTFORMLC [13] 天界、桃園
PRINTFORMLC [14] 桃林～砂浜
PRINTFORMLC [31] 霧の湖、湖畔(紅魔館)
PRINTFORMLC [32] 三途の川
PRINTFORMLC [33] 玄武の沢
PRINTFORMLC [34] 大蝦蟇の池
PRINTFORMLC [35] 九天の滝、滝の裏
PRINTFORMLC [36] 山の湖、湖畔（妖怪の山山頂）
PRINTFORMLC [37] 地獄の深道、川岸
PRINTFORMLC [61] 墓地
PRINTFORMLC [62] 無縁塚
PRINTFORMLC [63] キノコ群生地
PRINTFORMLC [64] 間欠泉地下センター
PRINTFORMLC [65] 灼熱地獄跡
PRINTFORMLC [66] 幻想風穴、縦穴の底
PRINTFORMLC [67] 静かの海
PRINTFORML 
DRAWLINE
PRINTFORML [99] やめる
INPUT
PLACE = RESULT
SELECTCASE PLACE
	CASE 99
		RETURN -1
	CASEELSE
		CALL SHOW_GATHERING_ITEM(PLACE)
		PRINTFORMW 
		CLEARLINE 1
		RESTART
ENDSELECT

@SHOW_GATHERING_LIST_SELECT(PLACE)
#DIM PLACE
DRAWLINE
PRINTFORML 採集可能物一覧
CALL COLORMESSAGE(@"中級素材　",C_YELLOW)
CALL COLORMESSAGE(@"高級素材　",C_P_GREEN)
CALL COLORMESSAGE(@"最高級素材",C_AQUA)
PRINTFORML 
DRAWLINE

SELECTCASE PLACE
	CASE 99
		RETURN -1
	CASEELSE
		CALL SHOW_GATHERING_ITEM(PLACE)
		PRINTFORML
		DRAWLINE
		PRINTFORMW
ENDSELECT

@SHOW_GATHERING_ITEM(PLACE)
#DIM PLACE

FOR LOCAL,600,710
	IF GATHERING_PRODUCT(LOCAL, PLACE)
		LOCALS = %ITEMNAME:LOCAL%
		IF ITEMPRICE:LOCAL >= 1000
			SETCOLOR C_YELLOW
			SIF ABL:MASTER:교양 < 1
				LOCALS = ？？？
		ENDIF
		IF ITEMPRICE:LOCAL >= 3000
			SETCOLOR C_P_GREEN
			SIF ABL:MASTER:교양 < 2
				LOCALS = ？？？
		ENDIF
		IF ITEMPRICE:LOCAL >= 10000
			SETCOLOR C_AQUA
			SIF ABL:MASTER:교양 < 3
				LOCALS = ？？？
		ENDIF
		;季節条件を満たしていない場合は灰色表記に
		SIF !GATHERING_SEASON(LOCAL)
			SETCOLOR 0x606060

		PRINTFORM %LOCALS%　
	ENDIF
	RESETCOLOR
NEXT

;-----------------------------
;引数で指定した価格範囲内の素材の中からランダムに入手
;-----------------------------
@GetRandMaterial(QYT, PriceRange:0, PriceRange:1)
#DIM PriceRange, 2
#DIM cArray, 100
#DIM cNumber
#DIM Chosen
#DIM QYT
cNumber = 0
FOR LOCAL, 600, 700
	SIF ITEMNAME:LOCAL == ""
		CONTINUE
	;アイテム価格上限
	SIF ITEMPRICE:LOCAL > PriceRange:0
		CONTINUE
	;下限は省略可
	SIF ITEMPRICE:LOCAL < PriceRange:1
		CONTINUE
	SIF ITEM:LOCAL >= 99
		CONTINUE
	cArray:cNumber = LOCAL
	cNumber ++
NEXT
SIF !cNumber
	RETURN

FOR LOCAL,0,QYT
	Chosen = RAND:cNumber
	ITEM:Chosen ++
	CALL COLORMESSAGE(@"%ITEMNAME:Chosen%を手に入れた",C_YELLOW,1)
NEXT