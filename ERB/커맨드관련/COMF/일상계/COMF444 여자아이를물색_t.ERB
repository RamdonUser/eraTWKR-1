;-------------------------------------------------
;여자아이를 물색
;일상계 커맨드, 레벨 1
;-------------------------------------------------

@COM444
#DIMS YUKIZURI_RACE  ;생성하는 MOB소녀의 종류
IF FLAG:데이트상대
	PRINTFORMW 어마어마하게 귀여운 아가씨가 바로 옆에 있다
	RETURN -1
ENDIF

$BUSSYOKU
IF !FLAG:70
	TIME += 3
ELSE
	BASE:MASTER:TSP -= 10
ENDIF
PRINTFORML %마스터는% 귀여운 아가씨가 없는지 물색했다
PRINTFORMW ……저 아이는 어떨까?

SELECTCASE CAN_NANPA()
	CASE 1
		YUKIZURI_RACE '= TEXTR("마을 아가씨/유부녀")
ENDSELECT



;PRINTFORMW %YUKIZURI_RACE%
CALL MAKE_YUKIZURI(RANDOM_CHARANUM, YUKIZURI_RACE)

$CHOICE
CALL CHARA_STATE(RANDOM_CHARANUM)
CALL ASK_M("마음이 간다", 1,"다른 아가씨를 찾는다", 1,"직전의 아가씨를 찾는다", TFLAG:직전모브코맵 == CFLAG:MASTER:현재위치 /100,"이전 알게 된 아가씨를 찾는다", 1,"물색 중지", 1)
IF RESULT == 4
	CFLAG:RANDOM_CHARANUM:현재위치 = SUKIMA()
	CFLAG:RANDOM_CHARANUM:갈아입음실행 = 0
	CFLAG:RANDOM_CHARANUM:출금 = 2
	SIF TARGET == RANDOM_CHARANUM
		TARGET = 0
	RETURN -1
ENDIF
IF RESULT == 1
	TFLAG:직전모브코맵 = CFLAG:MASTER:현재위치 /100
	SAVECHARA "TEMP_MOB", "직전에 만난 아이", RANDOM_CHARANUM
	GOTO BUSSYOKU
ELSEIF RESULT == 3
	CALL LOAD_MOBGIRL
	IF RESULT == -1
		;만약을 위해 신품으로 해 둔다
		DELCHARA RANDOM_CHARANUM
		ADDCHARA RANDOM_CHARANUM
		RETURN -1
	ENDIF
ELSEIF RESULT == 2
	DELCHARA RANDOM_CHARANUM
	LOADCHARA "TEMP_MOB"
	TFLAG:직전모브코맵 = 0
	GOTO CHOICE
ENDIF
CFLAG:RANDOM_CHARANUM:현재위치 = CFLAG:MASTER:현재위치
IF CFLAG:MASTER:현재위치 / 100 == MAIN_MAP
	CFLAG:RANDOM_CHARANUM:데이트중 = CFLAG:MASTER:현재위치 / 100
	CFLAG:RANDOM_CHARANUM:거점내방 = 1
ELSE
	CFLAG:RANDOM_CHARANUM:만난위치 = CFLAG:MASTER:현재위치
ENDIF
CFLAG:RANDOM_CHARANUM:데일리 = 30
TARGET = RANDOM_CHARANUM
RETURN 1


@COM_ABLE444
;일괄관리
SIF GLOBAL_COMABLE(622)
	RETURN RESULT
SIF !CAN_NANPA()
	RETURN 0
SIF !INRANGE(TIME, 540,1140)
	RETURN 0
SIF !CFLAG:RANDOM_CHARANUM:출금
	RETURN 0
RETURN 1



@PRINT_SAVED_GIRL_LIST
DRAWLINE
FOR LOCAL,0,MAXMOB
	IF FIND_CHARADATA(TOSTR(LOCAL))
		CHKCHARADATA RESULTS:0
		PRINTBUTTON @"[%RESULTS:0%]", LOCAL
		PRINTFORML 
	ELSE
		PRINTBUTTON "[없음]", LOCAL
		PRINTFORML 
	ENDIF
NEXT
DRAWLINE
PRINTBUTTON "[그만둔다]", 99

@LOAD_MOBGIRL
CALL PRINT_SAVED_GIRL_LIST
INPUT
SIF !INRANGE(RESULT, 0, MAXMOB -1)
	RETURN -1
IF MOBGIRL_PLACE:RESULT != CFLAG:MASTER:현재위치 / 100
	PRINTFORMW 여기에는 없을 것이다
	RETURN -1
ENDIF
IF FIND_CHARADATA(TOSTR(RESULT))
	DELCHARA RANDOM_CHARANUM
	LOADCHARA TOSTR(RESULT)
ELSE
	RETURN -1
ENDIF

@SAVE_MOBGIRL
#DIM CHOSEN_MOB
#DIMS MOBNAME

$LOOP
CALL PRINT_SAVED_GIRL_LIST
INPUT
SIF RESULT == 99
	RETURN -1
SIF !INRANGE(RESULT, 0, MAXMOB - 1)
	GOTO LOOP

CHOSEN_MOB = RESULT
IF FIND_CHARADATA(TOSTR(CHOSEN_MOB))
	CHKCHARADATA RESULTS:0
	STRFIND RESULTS:0, "：" 
	SUBSTRING RESULTS:0, 0, RESULT
	MOBNAME = %RESULTS:0%
	PRINTFORML %조사처리(MOBNAME,"를")% 잊어, 대신에 %조사처리(CALLNAME:RANDOM_CHARANUM,"를")% 기억합니까?
	CALL ASK_YN
	IF !RESULT
		PRINTFORM %마스터는% 몰인정하게도 %조사처리(MOBNAME,"를")% 깨끗히 잊고, 
	ELSE
		GOTO LOOP
	ENDIF
ENDIF
MOBGIRL_PLACE:CHOSEN_MOB = CFLAG:RANDOM_CHARANUM:자주가는지역
SAVECHARA TOSTR(CHOSEN_MOB), CSTR:RANDOM_CHARANUM:모브코프로필, RANDOM_CHARANUM
PRINTFORMW %조사처리(CALLNAME:RANDOM_CHARANUM,"를")% 기억했다