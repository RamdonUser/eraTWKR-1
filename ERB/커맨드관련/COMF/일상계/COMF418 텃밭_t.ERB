;-------------------------------------------------
;텃밭
;일상계 커맨드, 레벨 1
;-------------------------------------------------
@COM418
LOCAL:1 = 1 + FLAG:밭확장
FOR LOCAL, 0, LOCAL:1
	CALL SHOW_PLANT(LOCAL)
NEXT
PRINTL [999] 그만둔다
$INPUT_LOOP
INPUT
IF INRANGE(RESULT / 100, 0, LOCAL:1)
	CALL FARMING(RESULT / 100, RESULT % 100)
ELSE
	TIME += 5
	RETURN 0
ENDIF


@FARMING(ARG, 행동번호)
#DIM 행동번호
LOCALS = %HATAKE_NAME:ARG%
IF 행동번호 == 0
	IF PLANT_TYPE:ARG > 0
		PRINTFORML 이미 %LOCALS%가 심어지고 있다
		TIME += 5
	ELSE
		PRINTL 어떤 것을 심습니까?
		CALL INPLANT(ARG)
		TIME += 10
	ENDIF
ELSEIF 행동번호 == 1
	IF !PLANT_DRY:ARG
		PRINTL 오늘은 이제 괜찮겠지
	ELSEIF !TFLAG:물긷기
		PRINTL 물를 길어 와야해
	ELSE
		PLANT_DRY:ARG = 0
		PRINTFORML %LOCALS%에 물을 주었다
		SIF ITEM:스와코의선물
			FLAG:천적곤신 = 1
		TIME += 5
	ENDIF
ELSEIF 행동번호 == 2
	SIF PLANT_DAYS:ARG > 9
		PLANT_GROW:ARG += 2
	SIF PLANT_FOOD:ARG
		PLANT_GROW:ARG ++
	SIF ITEM:스와코의선물
		PLANT_GROW:ARG += 2									  
	;大豊作…天滴とこやし補正でないと到達不能
	IF PLANT_GROW:ARG > 12
		PRINTL 대풍작이다!
		IF PLANT_FOOD:ARG
			LOCAL = 14 + RAND:6
		ELSE
			LOCAL = 14
		ENDIF
	ELSEIF PLANT_GROW:ARG > 10
		PRINTL 풍작이다!
		IF PLANT_FOOD:ARG
			LOCAL = 10 + RAND:4
		ELSE
			LOCAL = 10
		ENDIF
	ELSEIF PLANT_GROW:ARG > 7
		IF PLANT_FOOD:ARG
			LOCAL = 5 + RAND:3
		ELSE
			LOCAL = 5
		ENDIF
	ELSE
		LOCAL = 3
	ENDIF
	PRINTFORML {LOCAL}개의 %LOCALS%를 수확했다
	IF PLANT_TYPE:0 == 190
		ITEM:0 += LOCAL
	ELSE
		ITEM:(200 + PLANT_TYPE:ARG) += LOCAL
	ENDIF
	PLANT_TYPE:ARG = 0
	PLANT_DAYS:ARG = 0
	PLANT_GROW:ARG = 0
	PLANT_DRY:ARG = 0
	HATAKE_NAME:ARG =
	PLANT_FOOD:ARG = 0
	TIME += 20
ELSEIF 행동번호 == 3
	IF PLANT_DRY:ARG <= 3 && PLANT_GROW:ARG <= 10
		PRINTFORML %LOCALS%를 뽑아냅니까?
		CALL ASK_YN
		IF RESULT
			TIME += 5
			RETURN 0
		ENDIF
	ENDIF
	PLANT_TYPE:ARG = 0
	PLANT_DAYS:ARG = 0
	PLANT_GROW:ARG = 0
	PLANT_DRY:ARG = 0
	HATAKE_NAME:ARG =
	PLANT_FOOD:ARG = 0
	PRINTFORML %LOCALS%를 뽑아냈다
	TIME += 10
;밭이 확장되면 전체로 합니다
ELSEIF 행동번호 == 4 && ITEM:간이풀장 && GETBIT (FLAG:풀장, 2)
	IF !PLANT_DRY:ARG
		PRINTL 오늘은 이제 괜찮겠지
	ELSE
		PLANT_DRY:ARG = 0
		PRINTFORML %LOCALS%에 물을 주었다
		SIF ITEM:스와코의선물
			FLAG:천적곤신 = 1
		TIME += 5
	ENDIF
ELSEIF 행동번호 == 5 && ITEM:195
	IF PLANT_FOOD:ARG
		PRINTL 비료를 너무 뿌리는 것도 좋지 않다
	ELSE
		PLANT_FOOD:ARG = 1
		PRINTFORML %LOCALS%에 비료를 뿌렸다
		TIME += 20
	ENDIF
ELSE
	TIME += 5
	RETURN 0
ENDIF

@INPLANT(ARG)
FOR LOCAL, 151,195	;거름주기 이후를 제외
	SIF ITEM:LOCAL
		PRINTFORML [{LOCAL}]%ITEMNAME:(LOCAL)%
NEXT
PRINTL [999] 그만둔다
$INPUT_LOOP
INPUT
IF RESULT >= 151 && RESULT <= 199 && ITEM:RESULT
	PRINTFORML 밭 {ARG}에 %ITEMNAME:(RESULT)%를 심었다
	ITEM:RESULT -= 1
	PLANT_TYPE:ARG = RESULT
	PLANT_DRY:ARG = 1
	;三粒の天滴を持っている場合、植えてすぐに施肥、水撒き完了状態に
	IF ITEM:스와코의선물
		PLANT_DRY:ARG = 0
		PLANT_FOOD:ARG = 1
		FLAG:천적곤신 = 1
	ENDIF
	IF PLANT_TYPE:ARG >= 190
		HATAKE_NAME:ARG = %ITEMNAME:(PLANT_TYPE:ARG - 190)%
	ELSE
		HATAKE_NAME:ARG = %STR:(3500 + PLANT_TYPE:ARG)%
	ENDIF
ELSE
	RETURN
ENDIF

@SHOW_PLANT(ARG)
PRINTFORM 밭 {ARG}：
IF PLANT_TYPE:ARG > 0
	SETCOLOR 0x00FF00
	PRINTFORM %HATAKE_NAME:ARG% 재배중
	IF PLANT_DRY:ARG > 3 || PLANT_GROW:ARG > 10
		SETCOLOR 0x994C00
		PRINT (마름)
	ELSE
		SELECTCASE PLANT_GROW:ARG
			CASE 10
				SETCOLOR 0xFF0000
				PRINT (익음)
			CASE 8,9
				SETCOLOR 0xFF8000
				PRINT (열매)
			CASE 6,7
				SETCOLOR 0xFFFF00
				PRINT (꽃)
			CASE 4,5
				PRINT (잎)
			CASEELSE
				PRINT (모종)
		ENDSELECT 
	ENDIF
	IF !PLANT_DRY:ARG
		SETCOLOR C_AQUA
		PRINT 　물 뿌리기가 끝난 상태
	ENDIF
	IF PLANT_FOOD:ARG
		SETCOLOR C_CREAM
		IF ITEM:스와코의선물
			PRINT 　곤신의 축복이 느껴진다
		ELSE
			PRINT   거름주기가 끝난 상태
		ENDIF
	ENDIF
	RESETCOLOR
	PRINTL
	;물 뿌리기
	IF PLANT_DRY:ARG
		LOCAL = ARG * 100 + 1
		LOCALS = [%TOSTR(LOCAL, "000")%] - 물을 뿌린다
		PRINTBUTTONLC LOCALS, LOCAL
	ELSE
		PRINTLC 　
	ENDIF
	;수확
	IF PLANT_GROW:ARG > 7 && !(PLANT_DRY:ARG > 3 || PLANT_GROW:ARG > 10)
		LOCAL = ARG * 100 + 2
		LOCALS = [%TOSTR(LOCAL, "000")%] - 수확한다
		SIF PLANT_GROW:ARG == 10
			SETCOLOR 0xFF0000
		PRINTBUTTONLC LOCALS, LOCAL
		RESETCOLOR
	ELSE
		PRINTLC 　
	ENDIF
	;정지
	LOCAL = ARG * 100 + 3
	LOCALS = [%TOSTR(LOCAL, "000")%] - 뽑아낸다
	PRINTBUTTONLC LOCALS, ARG * 100 + 03
	;스프링 쿨러
	IF ITEM:간이풀장 && GETBIT (FLAG:풀장, 2) && PLANT_DRY:ARG
		LOCAL = ARG * 100 + 4
		LOCALS = [%TOSTR(LOCAL, "000")%] - 스프링 쿨러
		PRINTBUTTONLC LOCALS, LOCAL
	ELSE
		PRINTLC 　
	ENDIF
	;비료를 뿌린다
	IF ITEM:195 && PLANT_FOOD:ARG == 0
		LOCAL = ARG * 100 + 5
		LOCALS = [%TOSTR(LOCAL, "000")%] - 비료를 뿌린다
		PRINTBUTTONLC LOCALS, LOCAL
	ELSE
		PRINTLC 　
	ENDIF
ELSE
	PRINTL 빈 곳
	;모종 심고
	LOCAL = ARG * 100 + 0
	LOCALS = [%TOSTR(LOCAL, "000")%] - 모종을 심는다
	PRINTBUTTONLC LOCALS, LOCAL
ENDIF
PRINTL

;-------------------------------------------------
;실행 판정
;-------------------------------------------------
@COM_ABLE418
;정지 중에는 불가
SIF FLAG:70 == 1
	RETURN 0
;청소 실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(418)
	RETURN RESULT
;뜰
SIF !텃밭가능(CFLAG:MASTER:현재위치)
	RETURN 0
;텃밭 세트
SIF !ITEM:150
	RETURN 0
;기력 0
SIF BASE:MASTER:기력 <= 0
	RETURN 0
SIF CFLAG:우후후 == 2
	RETURN 0
RETURN 1

