;-------------------------------------------------
;신장·체중·3 사이즈의 표시 처리
;-------------------------------------------------
;BASE:20…신장
;BASE:21…체중
;BASE:22…가슴
;BASE:23…웨스트
;BASE:24…히프
;MAXBASE:20…신장 계수
;MAXBASE:21…체중 계수
;MAXBASE:22…가슴 계수
;MAXBASE:23…웨스트 계수
;MAXBASE:24…히프 계수

@MAKE_SIZE, ARG
;신체 측정 플래그가 서 있는 경우는 계수를 생성
;신체 측정필 또한 미설정의 경우도 생성
;CSV에 기입되어 있지 않은 경우의 표준치는 1000
VARSET LOCAL
IF CFLAG:ARG:신체측정 == 2 || (CFLAG:ARG:신체측정 == 1 && (MAXBASE:ARG:신장 == 0 || MAXBASE:ARG:체중 == 0 || MAXBASE:ARG:가슴 == 0 || MAXBASE:ARG:웨스트 == 0 || MAXBASE:ARG:히프 == 0))
	FOR LOCAL, 20, 25
		;SIZE_DATA.ERB를 사용하는 경우
		SIF FLAG:신체측정데이터
			MAXBASE:ARG:LOCAL = SIZE_DATA(ARG, LOCAL)
		;캐릭터의 CSV에 데이터가 있으면 그 쪽을 우선
		SIF CSVBASE(ARG, LOCAL)
			MAXBASE:ARG:LOCAL = CSVBASE(ARG, LOCAL)
		SIF MAXBASE:ARG:LOCAL == 0
			MAXBASE:ARG:LOCAL = 1000
		MAXBASE:ARG:LOCAL = MAXBASE:ARG:LOCAL * RAND(950, 1050) / 1000
	NEXT
ENDIF

;===신장의 산출===
;기본치
BASE:ARG:신장 = (TALENT:ARG:성별 == 2) ? 1700 # 1500
BASE:ARG:신장 = BASE:ARG:신장 * MAXBASE:ARG:신장 / 1000

;체형 보정
;SIZE_DATA로 신장이 설정되어 있는 경우는 보정하지 않는다
IF !FLAG:신체측정데이터 || !SIZE_DATA(ARG, 20)
	IF TALENT:ARG:체형 == -2
		TIMES BASE:ARG:신장, 0.80
	ELSEIF TALENT:ARG:체형 == -1
		TIMES BASE:ARG:신장, 0.90
	ELSEIF TALENT:ARG:체형 == 1
		TIMES BASE:ARG:신장, 1.10
	ELSEIF TALENT:ARG:체형 == 2
		TIMES BASE:ARG:신장, 1.20
	ELSEIF TALENT:ARG:체형 == 3
		TIMES BASE:ARG:신장, 1.50
	ENDIF
ENDIF

;===웨스트의 산출===
;기본치
BASE:ARG:웨스트 = BASE:ARG:신장 * ((FLAG:신체측정개변 >= 2) ? 38 # 40) / 100
BASE:ARG:웨스트 = BASE:ARG:웨스트 * MAXBASE:ARG:웨스트 / 1000

;체형 보정
IF TALENT:ARG:체형 == -2
	TIMES BASE:ARG:웨스트, 1.16
ELSEIF TALENT:ARG:체형 == -1
	TIMES BASE:ARG:웨스트, 1.08
ENDIF

;그 외 보정
SIF TALENT:ARG:매력
	TIMES BASE:ARG:웨스트, 0.95

;===가슴의 산출===
;기본치
BASE:ARG:가슴 = BASE:ARG:신장 * ((FLAG:신체측정개변 >= 2) ? 54 # 51) / 100
BASE:ARG:가슴 = BASE:ARG:가슴 * MAXBASE:ARG:가슴 / 1000

;가슴 사이즈 보정
IF TALENT:ARG:가슴사이즈 == -2
	TIMES BASE:ARG:가슴, 0.84
ELSEIF TALENT:ARG:가슴사이즈 == -1
	TIMES BASE:ARG:가슴, 0.92
ELSEIF TALENT:ARG:가슴사이즈 == 1
	TIMES BASE:ARG:가슴, 1.08
ELSEIF TALENT:ARG:가슴사이즈 == 2
	TIMES BASE:ARG:가슴, 1.16
ENDIF

;B감각에 의한 보정
BASE:ARG:가슴 = BASE:ARG:가슴 * (1000 + MIN(ABL:ARG:B감각, 100)) / 1000

;그 외 보정
SIF TALENT:ARG:매력
	TIMES BASE:ARG:가슴, 1.05

;언더 가슴 이하라면 보정
BASE:ARG:가슴 = MAX(BASE:ARG:신장 * 43 / 100, BASE:ARG:가슴)

;【특수】컵수중시 설정의 경우의 계산
IF FLAG:신체측정개변 == 3
	;LOCAL:1 언더 가슴
	;LOCAL:2 컵수→가슴과 언더 가슴의 차분
	;언더 가슴을 먼저 계산
	LOCAL:1 = BASE:ARG:신장 * 44 / 100
	LOCAL:1 = LOCAL:1 * MAXBASE:ARG:가슴 / 1000
	;소질로부터 컵 사이즈를 결정한다
	SELECTCASE TALENT:ARG:가슴사이즈
		;절벽(AAA=1～ AA=2）
		CASE -2
			LOCAL:2 = 1		
		;작은 가슴(A=3～ B=4）
		CASE -1
			LOCAL:2 = 3
		;보유(C=5 ～ D=6）
		CASE 0
			LOCAL:2 = 5
		;거유(E=7 ～ G=9）
		CASE 1
			LOCAL:2 = 7
		;폭유(H=10 ～ ）
		CASEELSE
			LOCAL:2 = 10
	ENDSELECT
	;신장으로 보정
	SELECTCASE BASE:ARG:신장
		CASE IS <= 1350
			LOCAL:2 --
		CASE IS >= 1650 
			LOCAL:2 ++
	ENDSELECT
	;컵 1개에 대해 2.5cm
	LOCAL:2 *= 25
	;B감각으로 보정한다
	;다만, 상한은 소질로 제한된 컵수까지 한다
	SELECTCASE TALENT:ARG:가슴사이즈
		;절벽(AAA=1～ AA=2）, 작은 가슴(A=3～ B=4）, 보유(C=5 ～ D=6）
		CASE -2 TO O
			LOCAL:2 += MIN(ABL:ARG:B감각 * 5, 49)
		;거유(E=7 ～ G=9）
		CASE 1
			LOCAL:2+= MIN(ABL:ARG:B감각 * 5, 74)
		;폭유(H=10 ～ ）
		CASEELSE
			LOCAL:2+= ABL:ARG:B감각 * 5
	ENDSELECT
	;가슴의 결정
	BASE:ARG:가슴 = LOCAL:1 + LOCAL:2
ENDIF

;===히프의 산출===
;기본치
BASE:ARG:히프 = BASE:ARG:신장 * ((FLAG:신체측정개변 >= 2) ? 54 # 53) / 100
BASE:ARG:히프 = BASE:ARG:히프 * MAXBASE:ARG:히프 / 1000

;그 외 보정
SIF TALENT:ARG:매력
	TIMES BASE:ARG:히프, 1.05

;===체중의 산출===
;기본치
BASE:ARG:체중 = BASE:ARG:신장 * BASE:ARG:신장 / 5000
BASE:ARG:체중 = BASE:ARG:체중 * MAXBASE:ARG:체중 / 1000

;체형 보정
IF TALENT:ARG:체형 == -2
	TIMES BASE:ARG:체중, 0.80
ELSEIF TALENT:ARG:체형 == -1
	TIMES BASE:ARG:체중, 0.90
ELSEIF TALENT:ARG:체형 == 1
	TIMES BASE:ARG:체중, 1.10
ELSEIF TALENT:ARG:체형 == 2
	TIMES BASE:ARG:체중, 1.20
ELSEIF TALENT:ARG:체형 == 3
	TIMES BASE:ARG:체중, 1.50
ENDIF

;그 외 보정
SIF TALENT:ARG:매력
	TIMES BASE:ARG:체중, 0.95

@SHOW_SIZE, ARG
;LOCAL:21…임신시의 체중의 계산치
;LOCAL:23…임신시의 웨스트의 계산치
VARSET LOCAL
CALL MAKE_SIZE, ARG
IF CFLAG:ARG:신체측정 == 0 && ARG != NO:0
	PRINTL
	PRINTFORML □ 사이즈 □-------------------------------------------------------------------------
	PRINTL  미측정
ELSE
	PRINTL
	PRINT □ 
	CALL PRINTBUTTON_EX("[사이즈]", 160, TFLAG:160)
	PRINTL □------------------------------------------------------------------------
	IF !TFLAG:사이즈표시
		;플레이어의 신장·체중은 불명
		IF ARG == NO:0
			PRINTFORM  신장:???cm  체중:??kg
		ELSE
			;신장
			IF TALENT:ARG:체형 == -5
				;소인의 신장은 1/10
				PRINTFORM  신장:{BASE:ARG:신장 / 100}. {BASE:ARG:신장 % 100 / 10}{BASE:ARG:신장 % 10}cm
			ELSE
				PRINTFORM  신장:{BASE:ARG:신장 / 10}. {BASE:ARG:신장 % 10}cm
			ENDIF
			;체중
			IF TALENT:ARG:임신
				;신장 150 cm로 10 kg정도 증가
				LOCAL:21 = BASE:ARG:체중 + (BASE:ARG:신장 * BASE:ARG:신장 * CFLAG:ARG:임신경과일수 / 2250000)
				IF TALENT:ARG:체형 == -5
					PRINTFORM    체중:{LOCAL:21 / 10}. {LOCAL:21 % 10}g
					PRINTFORML  임신전({BASE:ARG:체중 / 10}. {BASE:ARG:체중 % 10}g)
				ELSE
					PRINTFORM    체중:{LOCAL:21 / 10}. {LOCAL:21 % 10}kg
					PRINTFORML  임신전({BASE:ARG:체중 / 10}. {BASE:ARG:체중 % 10}kg)
				ENDIF
			ELSE
				;소인의 체중은 1/10^3
				IF TALENT:ARG:체형 == -5
					PRINTFORM    체중:{BASE:ARG:체중 / 10}. {BASE:ARG:체중 % 10}g
				ELSE
					PRINTFORM    체중:{BASE:ARG:체중 / 10}. {BASE:ARG:체중 % 10}kg
				ENDIF
			ENDIF
		ENDIF
		;[남자]가 아닌 플레이어는 비표시는 아니고 불명
		IF ARG == NO:0 && TALENT:ARG:성별 != 2
			PRINTFORML    B:??cm/ W:??cm/ H:??cm
		;[남자]소유는 3 사이즈는 표시하지 않는다
		ELSEIF TALENT:ARG:성별 != 2
			;가슴
			IF TALENT:ARG:체형 == -5
				PRINTFORM    B:{BASE:ARG:가슴 / 100}. {BASE:ARG:가슴 % 100 / 10}{BASE:ARG:가슴 % 10}cm 
				PRINTFORM %LOCALS:0%
			ELSE
				PRINTFORM    B:{BASE:ARG:가슴 / 10}. {BASE:ARG:가슴 % 10}cm 
				PRINTFORM %LOCALS:0%
			ENDIF
			CALL PRINT_CUPSIZE , ARG
			;웨스트
			IF TALENT:ARG:임신
				;신장 150 cm로 30 cm정도 증가
				LOCAL:23 = BASE:ARG:체중 + (BASE:ARG:신장 * CFLAG:ARG:임신경과일수 / 500)
				IF TALENT:ARG:체형 == -5
					PRINTFORM /W:{LOCAL:23 / 100}. {LOCAL:23 % 100 / 10}{LOCAL:23 % 10}cm
					PRINTFORM  임신전({BASE:ARG:웨스트 / 100}. {BASE:ARG:웨스트 % 100 / 10}{BASE:ARG:웨스트 % 10}cm) /
				ELSE
					PRINTFORM /W:{LOCAL:23 / 10}. {LOCAL:23 % 10}cm
					PRINTFORM  임신전({BASE:ARG:웨스트 / 10}. {BASE:ARG:웨스트 % 10}cm) /
				ENDIF
			ELSE
				IF TALENT:ARG:체형 == -5
					PRINTFORM /W:{BASE:ARG:웨스트 / 100}. {BASE:ARG:웨스트 % 100 / 10}{BASE:ARG:웨스트 % 10}cm /
				ELSE
					PRINTFORM /W:{BASE:ARG:웨스트 / 10}. {BASE:ARG:웨스트 % 10}cm /
				ENDIF
			ENDIF
			;히프
			IF TALENT:ARG:체형 == -5
				PRINTFORML  H:{BASE:ARG:히프 / 100}. {BASE:ARG:히프 % 100 / 10}{BASE:ARG:히프 % 10}cm
			ELSE
				PRINTFORML  H:{BASE:ARG:히프 / 10}. {BASE:ARG:히프 % 10}cm
			ENDIF
		ENDIF
	ENDIF
ENDIF
PRINTL

@PRINT_CUPSIZE, ARG
;가슴의 컵 사이즈 생성
SPLIT "(-) , (AAA) , (AA) , (A) , (B) , (C) , (D) , (E) , (F) , (G) , (H) , (I) , (J) , (K) , (L) , (M) , (N) , (O) , (P) , (Q) , (R) , (S) , (T) , (U) , (V) , (W) , (X) , (Y) , (Z) , (Z+) , (규격외) ",",", LOCALS
;기본치
IF FLAG:신체측정개변 == 2
	LOCAL = (BASE:ARG:가슴 - (BASE:ARG:신장 * 44 / 100)) / 25
ELSEIF FLAG:신체측정개변 == 3
	LOCAL:1 = BASE:ARG:신장 * 44 / 100
	LOCAL:1 = LOCAL:1 * MAXBASE:ARG:가슴 / 1000
	LOCAL = (BASE:ARG:가슴 - LOCAL:1) / 25
ELSE
	LOCAL = (BASE:ARG:가슴 - (BASE:ARG:신장 * 43 / 100)) / 25
ENDIF

;소인은 1/10
SIF TALENT:ARG:체형 == -5
	TIMES LOCAL, 0.10

;캐릭터라인 변수가 약간 부자유스러운 것으로 억지로 작성
LOCAL = LIMIT(LOCAL, 0, 30)
LOCALS:0 = %LOCALS:(LOCAL)%
PRINTFORM %LOCALS:0%
